!!!! Turkish Phonological Rules

!!! Character Groups

define Alphabet29Lower	[a|b|c|ç|d|e|f|g|ğ|h|ı|i|j|k|l|m|n|o|ö|p|r|s|ş|t|u|ü|v|y|z];
define Alphabet29Upper	[A|B|C|Ç|D|E|F|G|Ğ|H|I|İ|J|K|L|M|N|O|Ö|P|R|S|Ş|T|U|Ü|V|Y|Z];
define Alphabet29	[Alphabet29Lower|Alphabet29Upper];

define CircumflexLower	[â|î|û];
define CircumflexUpper	[Â|Î|Û];
define Circumflex	[CircumflexLower|CircumflexUpper];
define Alphabet32	[Alphabet29|Circumflex];

define ForeignLower	[q|w|x];
define ForeignUpper	[Q|W|X];
define Foreign		[ForeignLower|ForeignUpper];
define Alphabet35	[Alphabet32|Foreign];


!!! Input Language:

!! stem
define StemPunc		['|%-];
define StemChar		[Alphabet32|StemPunc];

!! phonetic flags
define PhonFlag 	["^AR"|"^CD"|"^CV"|"^VD"|"^VF"|"^Y"];

!! apostrophe
define Apostrophe	['];

!! the P3 flag
define P3Flag		["^P3"];

!! End of stem
define StemEnd		[PhonFlag* (Apostrophe) (P3Flag) %+];

!! suffixes: each suffix starts with a "+" and symbols that consist of an equal sign and capital letter indicate phonetic groups
!! =A represents [ae]
!! =I represents [ıiuü]
!! =D represents [dt]
!! =C represents [cç]
!! =Y, =N, =S represent optional y,n,s characters

define PhonGroup	[=A|=C|=D|=I|=N|=S|=Y];
define SuffChar		[%+|PhonGroup|Alphabet29Lower];

define PhonInput	[ StemChar+ PhonFlag* (Apostrophe) (P3Flag) SuffChar* ];


!! Output Language:

define PhonOutput	[StemChar+ (Apostrophe) Alphabet29Lower*];


!!! Phonological Rules


#### Türk Dil Kurumu Yazım Kılavuzundan: (15 Eylül 2009)


### 1. Ses, Harf ve Alfabe

# Türk alfabesi, Latin harfleri esas alınarak 1.XI.1928 gün ve 1353
# sayılı Türk Harflerinin Kabul ve Tatbiki Hakkında Kanun ile kabul
# edilmiştir. Bu Kanun’a göre, Türk alfabesinde 29 harf bulunmaktadır.

### 2. Düzeltme İşareti

# Düzeltme işaretinin kullanılacağı yerler aşağıda gösterilmiştir:

## 2.1 Uzun okunuşlu ünlüler

# Yazılışları bir, anlamları ve okunuşları ayrı olan kelimeleri ayırt
# etmek için, okunuşları uzun olan ünlülerin üzerine konur: adem
# (yokluk), âdem (insan); adet (sayı), âdet (gelenek, alışkanlık);
# alem (bayrak), âlem (dünya, evren); alim (her şeyi bilen), âlim
# (bilgin); aşık (eklem kemiği), âşık (vurgun, tutkun); hakim (hikmet
# sahibi), hâkim (yargıç); hal (pazar yeri), hâl (durum, vaziyet);
# hala (babanın kız kardeşi), hâlâ (henüz); şura (şu yer), şûra
# (danışma kurulu).

# UYARI : Katil (< katl = öldürme) ve kadir (< kadr = değer)
# kelimeleriyle karışma olasılığı olduğu hâlde katil (ka:til =
# öldüren) ve kadir (< ka:dir = güçlü) kelimelerinin düzeltme işareti
# konmadan yazılması yaygınlaşmıştır.

## 2.2 Yabancı kelimeler ve özel isimler

# Arapça ve Farsçadan dilimize giren birtakım kelime ve eklerle özel
# adlarda bulunan ince g, k ünsüzlerinden sonra gelen a ve u ünlüleri
# üzerine konur: dergâh, gâvur, ordugâh, tezgâh, yadigâr, Nigâr;
# dükkân, hikâye, kâfir, kâğıt, Hakkâri, Kâzım, mahkûm, mekân, mezkûr,
# sükûn, sükût. Kişi ve yer adlarında ince l ünsüzünden sonra gelen a
# ve u ünlüleri de düzeltme işareti ile yazılır: Halûk, Lâle, Nalân;
# Balâ, Elâzığ, İslâhiye, Lâdik, Lâpseki.

## 2.3 Nispet i'si

# Nispet i'sinin belirtme durumu ve iyelik ekiyle karışmasını önlemek
# için kullanılır. Böylece (Türk) askeri ve askerî (okul), (İslam)
# dini ve dinî (bilgiler), (fizik) ilmi ve ilmî (tartışmalar),
# (Atatürk'ün) resmi ve resmî (kuruluşlar) gibi anlamları farklı
# kelimelerin karıştırılması da önlenmiş olur.

# Nispet i'si alan kelimelere Türkçe ekler getirildiğinde düzeltme
# işareti olduğu gibi kalır: millîleştirmek, millîlik, resmîleştirmek,
# resmîlik.



### 3. Ünlüler

define VowelLower	[a|e|ı|i|o|ö|u|ü|CircumflexLower];
define VowelUpper	[A|E|I|İ|O|Ö|U|Ü|CircumflexUpper];
define Vowel		[VowelLower|VowelUpper];
define NoVowel		[ \Vowel* ];

## 3.1 Ünlülerin Nitelikleri

# Ses yolunda herhangi bir engele çarpmadan çıkan seslere ünlü
# denir. Türkçede sekiz ünlü vardır: a, e, ı, i, o, ö, u, ü.

# Ünlüler şu biçimde sınıflandırılır:

# A. Çıkış yeri ve dilin durumuna göre:

#    1.        Kalın ünlüler: a, ı, o, u.
#    2.        İnce ünlüler: e, i, ö, ü.

# B. Dudakların durumuna göre:

#    1.        Düz ünlüler: a, e, ı, i.
#    2.        Yuvarlak ünlüler: o, ö, u, ü.

# C. Ağzın açıklığına göre:

#    1.        Geniş ünlüler: a, e, o, ö.
#    2.        Dar ünlüler: ı, i, u, ü.

define VFU [e|i|î|E|İ|Î];	# Front unrounded
define VFR [ö|ü|Ö|Ü];		# Front rounded
define VBU [a|â|ı|A|Â|I];	# Back unrounded
define VBR [o|u|û|O|U|Û];	# Back rounded
define VF  [VFU|VFR];		# Front
define VB  [VBU|VBR];		# Back


## 3.2 Büyük Ünlü Uyumu

# Bir kelimenin birinci hecesinde kalın bir ünlü (a, ı, o, u)
# bulunuyorsa, diğer hecelerdeki ünlüler de kalın; ince bir ünlü (e,
# i, ö, ü) bulunuyorsa diğer hecelerdeki ünlüler de ince olur: adım,
# ağız, ayak, boyun, boyunduruk, burun, dalga, dudak, duvak,
# kırlangıç; beşik, bilezik, gelincik, gözlük, üzengi, vergi,
# yüzük. Buna büyük ünlü uyumu adı verilir.

# Büyük ünlü uyumuna aykırı bazı Türkçe kelimeler de vardır: anne,
# dahi, elma, hangi, hani, inanmak, kardeş, şişman.

# Büyük ünlü uyumu alıntı kelimelerde aranmaz: ahenk, badem, ceylan,
# çiroz, dükkân, fidan, gazete, hamsi, kestane, limon, model, nişasta,
# pehlivan, selam, tiyatro, viraj, ziyaret.

# Birleşik kelimelerde büyük ünlü uyumu aranmaz: açıkgöz, bilgisayar,
# çekyat, hanımeli.

# -gil, -ken, -leyin, -mtırak, -yor ekleri büyük ünlü uyumuna uymaz:
# akşam-leyin, bakla-gil-ler, çalışır-ken, ekşi-mtırak, yürü-yor.

# -daş (-taş) eki bazı kelimelerde büyük ünlü uyumuna uymaz: din-daş,
# gönül-daş, meslek-taş, ülkü-daş.

# -ki aitlik eki büyük ünlü uyumuna uymaz: akşamki, yarınki,
# duvardaki, yoldaki, ondaki, yazıdaki, onunki.

# Büyük ünlü uyumuna girmeyen kelimelere gelen ekler, kalınlık incelik
# bakımından son hecenin ünlüsüne uyar: adalet-li, anne-si,
# kardeş-lik, meslektaş-ımız, şişman-lık.

define VowelHarmony [
    =A -> a // VB  NoVowel _ ,,
    =A -> e // VF  NoVowel _ ,,
    =I -> ı // VBU NoVowel _ ,,
    =I -> i // VFU NoVowel _ ,,
    =I -> u // VBR NoVowel _ ,,
    =I -> ü // VFR NoVowel _ 
];

# Son ünlüleri kalın sıradan olmasına karşın incelik özelliği gösteren
# bazı alıntı kelimeler ince ünlülü ekler alır: alkol / alkolü,
# hakikat / hakikati, helak / helakimiz, kabul / kabulü, kontrol /
# kontrolü, protokol / protokolü, saat / saate, sadakat / sadakatten.

!! We mark these exceptions in the lexicon with the ^VF flag:

define VFflag [ NoVowel "^VF" StemEnd NoVowel ];

define VowelFront [
    =A -> e // VB  VFflag _ ,,
    =I -> i // VBU VFflag _ ,,
    =I -> ü // VBR VFflag _
].o.[ "^VF" -> 0 ];


## 3.3 Küçük Ünlü Uyumu

# Küçük ünlü uyumu kuralı iki yönlüdür:

# 5.1. Bir kelimenin ilk hecesinde düz ünlü (a, e, ı, i) varsa
# sonraki hecelerde de düz ünlü bulunur: anlaşmak, yanaşmak, kayıkçı,
# ısırmak, ılıklaşmak, seslenmek, yelek, bilek, çilek.

# 5.2. Bir kelimenin ilk hecesinde yuvarlak ünlü (o, ö, u, ü) varsa
# bunu izleyen ilk hecede dar yuvarlak (u, ü) veya geniş düz (a, e)
# ünlü bulunur: boyunduruk, çocuk, odun, yorgunluk, yoklamak, vurmak,
# yumurta, özlemek, güreşmek, sürmek.

# Küçük ünlü uyumuna aykırı bazı Türkçe kelimeler de vardır: avuç,
# avurt, çamur, kabuk, kavuk, kavun, kavur-, kavuş-, savur-, yağmur.

# Küçük ünlü uyumu, alıntı kelimelerde aranmaz: aktör, alkol, bandrol,
# daktilo, doktor, horoz, kabul, kitap, konsolos, muzır, mühim, mümin,
# müzik, profesör, radyo, vakur.

# Küçük ünlü uyumuna aykırı kelimelere getirilen ekler, kelimenin son
# ünlüsüne uyar: kavun-u, konsolos-luğ-u, mümin-lik, müzik-çi,
# yağmur-luk.

# -ki aitlik eki yalnızca birkaç örnekte küçük ünlü uyumuna uyar:
# bugünkü, dünkü, öbürkü.

# Bu ünlü düzenleri ve ilk heceyi izleyen ünlüler aşağıdaki çizelgede
# gösterilmiştir:

# a › a, ı (bakar, alır)
# o › u, a (omuz, oya)
# e › e, i (geçer, gelir)
# ö › ü, e (ölçü, ördek)
# ı › ı, a (kılıç, kısa)
# u › u, a (uzun, uzak)
# i › i, e (ilik, ince)
# ü › ü, e (ütü, ürkek)  


## 3.4 Ünlü Daralması (a - ı, e – i)

# Türkçede a, e ünlüsü ile biten fiillerin şimdiki zaman çekiminde,
# söyleyiş ve yazılışta da a ünlüleri ı, u; e ünlüleri i, ü olur:
# başlıyor, kanıyor, oynuyor, doymuyor; izliyor, diyor, gelmiyor,
# gözlüyor.

# Birden çok heceli ve a, e ünlüleri ile biten fiiller, ünlüyle
# başlayan ek aldıklarında bu fiillerdeki a, e ünlülerinde söyleyişte
# yaygın bir daralma (ı ve i'ye dönme) eğilimi görülür. Ancak,
# söyleyişteki ı, i ünlüleri yazıya geçirilmez: başlayan, yaşayacak,
# atlayarak, saklayalı, atmayalım, gelmeyen, izlemeyecek, gitmeyerek,
# gizleyeli, besleyelim.

# Buna karşılık tek heceli olan demek ve yemek fiillerinde,
# söyleyişteki i ünlüsü yazıya da geçirilir: diyen, diyerek, diyecek,
# diyelim, diye; yiyen, yiyerek, yiyecek, yiyelim, yiye, yiyince,
# yiyip. Ancak deyince, deyip sözlerindeki e yazılışta korunur.

define VowelClosing [
    Vowel -> 0	|| _ StemEnd ? y o r
].o.[
    e -> i	|| .#. y _ StemEnd [=Y|y] ,,
    e -> i	|| .#. d _ StemEnd [=Y|y] \[=I|i] ,,
    =I -> i	|| .#. [d|y] StemEnd _ y o r
].o.[
    e -> 0	|| .#. [d|y] _ StemEnd =A r
];


## 3.5 Ünlü Düşmesi

# İkinci hecesinde dar ünlü bulunan iki heceli kelimeler ünlüyle
# başlayan bir ek aldıklarında ikinci hecelerindeki dar ünlüler
# genellikle düşer: ağız / ağzı, alın / alnı, bağır / bağra, bağrım,
# beniz / benzi, beyin / beynimiz, boyun / boynu, böğür / böğrüm,
# burun / burnu, geniz / genzi, göğüs / göğsün, gönül / gönlünüz,
# karın / karnı, oğul / oğlu; çevir- / çevril-, devir- / devril-.


!! We mark the vowel dropping words in the dictionary using the ^VD flag
define VDflag [ PhonFlag* "^VD" ];

!! Word end and suffix starting with vowel (no apostrophe)
define Vsuffix [ PhonFlag* (P3Flag) %+ (=Y|=N|=S) Vowel ];

define VowelDrop [
    Vowel -> 0 || _ NoVowel VDflag Vsuffix
].o.[
    "^VD" -> 0
];


!! Many suffixes that start with an optional =I drop it after a vowel

define VowelDropSuff [ =I -> 0 // Vowel StemEnd _ ];


### 4. Ünsüzler

define ConsLower	[b|c|ç|d|f|g|ğ|h|j|k|l|m|n|p|r|s|ş|t|v|y|z|q|w|x];
define ConsUpper	[B|C|Ç|D|F|G|Ğ|H|J|K|L|M|N|P|R|S|Ş|T|V|Y|Z|Q|W|X];
define Cons		[ConsLower|ConsUpper];

## 4.1 Ünsüzlerin Nitelikleri

# Ses yolunda bir engele çarparak çıkan seslere ünsüz denir.

# Dilimizde yirmi bir ünsüz vardır. Bunlar: b, c, ç, d, f, g, ğ, h, j,
# k, l, m, n, p, r, s, ş, t, v, y, z.

# Ünsüzler ses tellerinin titreşime uğrayıp uğramamasına göre iki
# gruba ayrılır:

# 1. Ses tellerinin titreşmesiyle oluşan ünsüzlere tonlu (yumuşak)
# ünsüzler adı verilir: b, c, d, g, ğ, j, l, m, n, r, v, y, z.

define CV [b|c|d|g|ğ|j|l|m|n|r|v|y|z]; # Voiced consonant

# 2. Ses telleri titreşmeden oluşan ünsüzlere tonsuz (sert) ünsüzler
# denir: ç, f, h, k, p, s, ş, t.

define CU [ç|f|h|k|p|s|ş|t];	# Unvoiced consonant


## 4.2 Ünsüz Tonlanması

# Kökeni Türkçe olan kelimelerin sonunda b, c, d, g ünsüzleri
# bulunmaz. Ancak, anlam farkını belirtmek üzere ad, od, sac gibi
# birkaç kelimenin yazılışında buna uyulmaz: ad (isim), at (binek
# hayvanı); od (ateş), ot (bitki); sac (yassı demir), saç (kıl).

define CVP [b|c|d|g];		# Voiced plosives
define CUP [p|ç|t|k];		# Unvoiced plosives

# Dilimizdeki hac, şad, yâd gibi birkaç örnek dışında, alıntı
# kelimelerde tonsuzlaşma kuralına uyulmuştur: sebep (< sebeb), kitap
# (< kitab), bent (< bend), cilt (< cild), bant (< band), etüt (<
# etüd), metot (< metod), standart (< standard), ahenk (< aheng),
# hevenk (< aveng), renk (< reng). Bu gibi alıntılar ünlü ile başlayan
# bir ek aldıklarında kelime sonundaki tonsuz ünsüzler tonlulaşır:
# sebep / sebebi, kitap / kitabı, bent / bendi, cilt / cildi, etüt /
# etüdü, metot / metodu, ahenk / ahengi, hevenk / hevengi, renk /
# rengi.

# UYARI : Bazı alıntı kelimelerde tonlulaşma (yumuşama) olmaz: ahlak /
# ahlakın, cumhuriyet / cumhuriyete, evrak / evrakı, hukuk / hukuku,
# ittifak / ittifaka, sepet / sepeti, tank / tankı, bank / bankı.

# Birden fazla heceli kelimelerin sonunda bulunan p, ç, t, k ünsüzleri
# ünlüyle başlayan bir ek aldığında tonlulaşarak b, c, d, ğ'ye
# dönüşür: kelep / kelebi; ağaç / ağacı, kazanç / kazancı; geçit /
# geçidi, kanat / kanadı; başak / başağı, bıçak / bıçağı, çocuk /
# çocuğu, dudak / dudağı, durak / durağı, uzak / uzağı.

# Tek heceli kelimelerin sonunda bulunan p, ç, t, k ünsüzleri ise iki
# ünlü arasında çoğunlukla korunur: ak / akı; at / atı; ek / eki; et /
# eti; göç / göçü; ip / ipi; kaç / kaçıncı; kök / kökü; ok / oku; ot /
# otu; saç / saçı; sap / sapı; suç / suçu; süt / sütü; üç /
# üçü. Ancak, tek heceli olduğu hâlde sonundaki ünsüzü tonlulaşan
# kelimeler de vardır: but / budu, dip / dibi, gök / göğü, kap / kabı,
# kurt / kurdu, uç / ucu, yurt / yurdu.

!! We represent stems that voice their final consonants in the dictionary using the ^CV flag

define CVflag [ PhonFlag* "^CV" ];

define ConsVoicing [
    k -> g, K -> G ||  [n|N] _ CVflag Vsuffix
].o.[
    p -> b, ç -> c, t -> d, k -> ğ,
    P -> B, Ç -> C, T -> D, K -> Ğ  || _ CVflag Vsuffix
].o.[
    "^CV" -> 0
];

!! Consonant voicing also applies to suffixes that end in k

define ConsVoicingSuff [
    k -> ğ  || %+ \%++ _ %+ (=Y|=N|=S) [Vowel|=A|=I]
];


## 4.3 Ünsüz Uyumu

# Dilimizde tonsuz (sert) ünsüzle biten kelimelere gelen ekler tonsuz
# (sert) ünsüzle başlar: aç-tı, aş-çı, bak-tım, bas-kı, çiçek-ten,
# düş-kün, geç-tim, ipek-çi, seç-kin, seç-ti, süt-çü.

define ConsUnvoicing [
	=D -> t, =C -> ç // CU StemEnd (=Y|=S|=N) _
].o.[
	=D -> d, =C -> c // StemEnd ?* _
];


## 4.4 Ünsüz Türemesi (y, v)

# İki ünlünün yan yana bulunduğu bazı alıntı kelimelerde ünlüler
# arasında y, v sesleri türemiştir: fiyat (< fiat), zayıf (< zaif);
# konservatuvar, laboratuvar, pisuvar, repertuvar, tretuvar, tuval,
# tuvalet.  

## 4.5 Ünsüz Düşmesi

# Arapçadan dilimize girmiş olan ve sonunda ikiz ünsüz bulunan
# kelimelerin yalın durumunda ünsüzlerden biri düşer (ünsüz tekleşir):
# hak (< hakk), his (< hiss), ret (< redd), zan (< zann), zem (<
# zemm). Bu tür kelimelere ünlüyle başlayan bir ek geldiğinde düşen
# ünsüz ortaya çıkar: hak, hakka; his, hissimiz; ret, reddi; zan,
# zannımca; zem, zemmi.


!! We mark these consonant doubling exceptions using the ^CD flag in the lexicon

define CDflag [ PhonFlag* "^CD" ];

define ConsDoubling [
    b->{bb}, c->{cc}, ç->{çç}, d->{dd}, f->{ff}, g->{gg}, ğ->{ğğ},
    h->{hh}, j->{jj}, k->{kk}, l->{ll}, m->{mm}, n->{nn}, p->{pp},
    r->{rr}, s->{ss}, ş->{şş}, t->{tt}, v->{vv}, y->{yy}, z->{zz},
    B->{BB}, C->{CC}, Ç->{ÇÇ}, D->{DD}, F->{FF}, G->{GG}, Ğ->{ĞĞ},
    H->{HH}, J->{JJ}, K->{KK}, L->{LL}, M->{MM}, N->{NN}, P->{PP},
    R->{RR}, S->{SS}, Ş->{ŞŞ}, T->{TT}, V->{VV}, Y->{YY}, Z->{ZZ}
    || _ CDflag Vsuffix
].o.[
    "^CD" -> 0
];

## 4.6 n > m Değişmesi

# Türkçede kullanılan bazı kelimelerdeki b ünsüzünden önce gelen n
# ünsüzü m'ye dönüşür: saklambaç (< saklanbaç), dolambaç (< dolanbaç),
# ambar (< anbar), amber (< anber), cambaz (< canbaz), çember (<
# çenber), kümbet (< gunbed), memba (< menba), mümbit (< munbit),
# tambur (< tanbur).


!! The rest does not seem to be covered by TDK, so we make it up until we find a better reference:

## 5. Kaynaştırma harfleri:

define Optionals [
    =Y -> y, =N -> n, =S -> s  || Vowel StemEnd _
].o.[
    [=Y|=N|=S] -> 0 || StemEnd _
];


!! Stuff below is increasingly unclean.  May redesign.

## 6. Irregular +n and +y insertion

define Ninsert [
    [ %+ =S =I ] -> [ %+ =S =I n ],
    [ %+ l =A r =I ] -> [ %+ l =A r =I n ],
    [ %+ k i ] -> [ %+ k i n ],
    "^P3" -> [ %+ n ] ||
    _ [[ %+ =D =A n ]|[ %+ =Y =I ]|[ %+ =Y =A ]|[ %+ =C =A ]|[ %+ =D =A ]] [.#.|%+]
];

define Yinsert [
    "^Y"      -> [ %+ y ] ||
    _ [[ %+ =I m =I z ]|[ %+ =I m ]|[ %+ =I n =I z ]|[ %+ =I n ]|[ %+ =S =I ]|[ %+ =N =I n ]],
    _ "^P3"
.o. "^Y" -> 0
.o. "^P3" -> 0
];


## 7. Handle irregular Aorist, Causative and Passive suffixes

define AltAor [			!! can make this more rule based
     [ %+ =A =I r ] -> [ %+ =A r ]	|| "^AR" _
.o.  [ %+ =A =I r ] -> [ %+ =I r ]
.o.  "^AR" -> 0
];

define AltPass [		# Deafult: +Il turns into +In or +InIl
    [ %+ =I l ] -> [ %+ =I n (=I l) ] || [Vowel|l|L] _
];

define AltCaus [		# Default: +DIr
    [ %+ =D =I r t ] -> [ %+ t ]	|| Vowel ?* Vowel (l|r) _
.o. [ %+ =D =I r t ] -> [ %+ =D =I r ]	|| .#. \%++ _
    # Set repeating Caus suffixes
.o. [ %+ =D =I r t ] -> [ %+ t ]	// r _ ,,
    [ %+ =D =I r t ] -> [ %+ =D =I r ]	// \r _
];


define Irregular [
Ninsert .o.
Yinsert .o.
AltAor .o.
AltPass .o.
AltCaus
];

define Phonetics [
VowelClosing .o.		# modifies stem ending vowel
VowelDropSuff .o.		# drops suffix initial =I after vowel
VowelFront .o.			# handles vowel harmony exceptions; removes ^VF
VowelHarmony .o.		# assigns =A, =I
VowelDrop .o.			# must come after VowelHarmony, removes ^VD
ConsUnvoicing .o.		# assigns =D, =C
ConsVoicing .o. 		# modifies stem ending consonant, removes ^CV
ConsVoicingSuff .o.		# modifies suffix ending k
ConsDoubling .o.		# must come after ConsVoicing, removes ^CD
Optionals			# assigns =Y, =N, =S
];

read regex
Irregular .o.
Phonetics .o.
[ %_ -> " " ] .o.		# convert multiword underscores to spaces
[ %+ -> 0 ];  			# get rid of inter-suffix markers
