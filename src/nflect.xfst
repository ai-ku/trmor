### nflect.xfst: defines all inflections and some derivations for
### nouns.  In particular all sequences except cyclic derivational
### sequences following the Nominative case are defined here.


### 0. Basic noun inflection consists of count, possessive and case
### markers: e.g. parti-ler-im-e

define Ncount	["+A3sg"|"+A3pl"];
define Nposs	["+P1sg"|"+P2sg"|"+P3sg"|"+P1pl"|"+P2pl"|"+P3pl"|"+Pnon"];
define Ncase	["+Abl"|"+Acc"|"+Dat"|"+Equ"|"+Gen"|"+Ins"|"+Loc"|"+Nom"];


### 1. The inflected noun may be followed by copular verb + person
### suffixes, except after the Accusative and Equative cases.
### +Past:di +Narr:miş +Cond:se +Pres:0 (+Cop:dir)
# e.g. parti-ler-im-de-ydi

read lexc vzero.lexc
upper net
define Vflect
define VerbDB	["^DB""+Verb""+Zero"];
define NcaseV	[Ncase - ["+Acc"|"+Equ"]];
define Vzero	[NcaseV VerbDB Vflect];


### 2. The Locative (and for some time words like "şimdi" the
### Nominative) case may be optionally followed by an Adjective
### Relative suffix -ki and further nominalization:
# e.g. parti-de-ki-ler-i

define NounDB	["^DB""+Noun""+Zero"];
define AdjDB	["^DB""+Adj""+Rel"];
define AdjRel	["+Loc" AdjDB (NounDB Ncount Nposs Ncase)];


### 3. The Genitive case may be optionally followed by a Pronoun
### Relative suffix -ki and further noun inflection.  Not all
### possessives are allowed after the Pronoun Relative.
# e.g. parti-nin-ki-ler-le

define PronDB	["^DB""+Pron""+Rel"];
define NpossP	["+P3sg"|"+Pnon"];
define PronRel	["+Gen" PronDB Ncount NpossP [Ncase | Vzero]];


### Here is the basic inflection skeleton:

define Nflect 	[Ncount Nposs [Ncase | Vzero | AdjRel | PronRel]];


### EXCEPTIONS:

## 1.1 Dative cannot be followed by a +Pres+A3pl
# e.g. !partineler

define PresA3pl	[VerbDB "+Pres" "+A3pl"];
define Nflect	[Nflect & ~$["+Dat" PresA3pl]];


## 1.2 No +Pres+A3pl after genitive:
# e.g. !sepetinler

define Nflect	[Nflect & ~$["+Gen" PresA3pl]];


## 1.3 Singular possessive cannot be followed by +Pres+A3pl
# e.g. !partimler

define P123sg	["+P1sg"|"+P2sg"|"+P3sg"];
define Nflect	[Nflect & ~$["+A3sg" P123sg "+Nom" PresA3pl]];


## 1.4 Plural nominative cannot be followed by +Pres+A3pl
# e.g. imzalarlar: this is almost always from "imzala" root.
!! It is not clear if we should restrict this to Pnon or not.
!! It seems to be ok when there is a cop 

#define PluNom	["+A3pl" "+Pnon" "+Nom"];
define PluNom	["+A3pl" ? "+Nom"];
define Nflect	[Nflect & ~$[PluNom PresA3pl]];

## 1.4 Plain plural cannot be followed by 1st 2nd person
# singular, (or 3rd person copular?)
# e.g. !partilerdim, !partilerdin

!? Shouldn't +A3sg also be illegal?  partilerdi(r) should make both
!? the noun and the verb plural.  In fact sepetlerdir is tagged as:
!? e.g. sepetlerdir	sepet	+Noun+A3sg+Pnon+Nom^DB+Verb+Zero+Pres+A3pl+Cop
!? So this is just disambiguation.  But proper nouns don't match:
!? e.g. DGM'lerdir	Dgm+Noun+Prop+A3pl+Pnon+Nom^DB+Verb+Zero+Pres+A3sg+Cop
!? which causes the one missing case.

define Plural	["+A3pl" "+Pnon" "+Nom"];
#define A123sg	["+A1sg"|"+A2sg"|"+A3sg""+Cop"];
define A12sg	["+A1sg"|"+A2sg"];
define Nflect	[Nflect & ~$[Plural VerbDB ? A12sg]];


## 1.5 No while after plural
# e.g. !partilerken
!? Why? We have yüzlülerken without a problem...

define AdverbDB	["^DB""+Adverb""+While"];
define Nflect	[Nflect & ~$[Plural VerbDB AdverbDB]];


## 1.6 No +Pres+A1pl after singular +P12sg:
# e.g. partimizdir => +Noun+A3sg+P1pl+Nom^DB+Verb+Zero+Pres+A3sg+Cop

define P12sg	["+P1sg"|"+P2sg"];
define PresA1pl	["+Pres" "+A1pl"];
define Nflect	[Nflect & ~$["+A3sg" P12sg "+Nom" VerbDB PresA1pl]];



## 2.1 No singular equative case:
# !partidekince
!? why??

define Nflect	[Nflect & ~$[NounDB "+A3sg" ? "+Equ"]];

## 2.2 No unmarked noun derivation

define Nflect	[Nflect & ~$[NounDB "+A3sg""+Pnon""+Nom"]];


### The following filters depend on dictionary flags and should be
### applied after the network has been concatenated with the
### dictionary.

define NoDB \"^DB"*;

define Nfilter [

## 0.1 Some words are inherently plural
# e.g. amcamlar, bakliyat

~$["+PL" NoDB "+A3sg"] &
~$["+PL" NoDB VerbDB PresA3pl] &

## 0.2 Some words come with possessive
# e.g. amcamlar, amcanlar

~$["+P1" NoDB Ncount \"P1sg"] &
~$["+P2" NoDB Ncount \"P2sg"] &

## 0.3 Some time words do not take AdjRel
# BUG: this rules out sabahki trying to fix sabahtaki
~$["+TM" NoDB "+Loc""^DB""+Adj""+Rel"] 

];

push Nflect
