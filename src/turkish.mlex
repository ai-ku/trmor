### Verb morphemes

define VerbRoot0 [
	"+Verb" -> {gül} || .#. _
];

define DefaultVerbMorphemes3 [
	"+A1sg" -> {+yHm},
	"+A2sg" -> {+sHn},
	"+A3sg" -> 0,
	"+A1pl" -> {+yHz},
	"+A2pl" -> {+sHnHz},
	"+A3pl" -> {+lAr},

	"+Aor" -> {+Hr},
	"+Cond" -> {+ysA},
	"+Cop" -> {+DHr},
	"+Desr" -> {+sA},
	"+Fut" -> {+yAcAk},
	"+Imp" -> 0,
	"+Imp2" -> {+sA},
	"+Narr" -> {+mHş},
	"+Narr2" -> {+ymHş},
	"+Neces" -> {+mAlH},
	"+Opt" -> {+yA},
	"+Past" -> {+DH},
	"+Past2" -> {+yDH},
	"+Prog1" -> {+Hyor},
	"+Prog2" -> {+mAktA},

	"+Neg" -> {+mA},
	"+Pos" -> 0,

	"^DB""+Verb""+Caus" -> {+t},
	"^DB""+Verb""+Pass" -> {+Hl},
	"^DB""+Verb""+Recip" -> {+Hş},
	"^DB""+Verb""+Reflex" -> {+Hn},

	"^DB""+Verb""+Able" -> {+yAbil},
	"^DB""+Verb""+Almost" -> {+yAyaz},
	"^DB""+Verb""+EverSince" -> {+yAgel},
	"^DB""+Verb""+Hastily" -> {+yHver},
	"^DB""+Verb""+Repeat" -> {+yAdur},
	"^DB""+Verb""+Start" -> {+yAkoy},
	"^DB""+Verb""+Stay" -> {+yAkal},

	"^DB""+Adverb""+Adamantly" -> {+yAsHyA},
	"^DB""+Adverb""+AfterDoingSo" -> {+yHp},
	"^DB""+Adverb""+AsIf" -> {+CAsHnA},
	"^DB""+Adverb""+AsLongAs" -> {+DHkçA},
	"^DB""+Adverb""+ByDoingSo" -> {+yArAk},
	"^DB""+Adverb""+SinceDoingSo" -> {+yAlH},
	"^DB""+Adverb""+When" -> {+yHncA},
	"^DB""+Adverb""+While" -> {+yken},
	"^DB""+Adverb""+WithoutBeingAbleToHaveDoneSo" -> {+yAmAdAn},
	"^DB""+Adverb""+WithoutHavingDoneSo" -> {+mAdAn},
	"^DB""+Adverb""+WithoutHavingDoneSo2" -> {+mAksHzHn},  !! should name this something else

	"^DB""+Adj""+Agt" -> {+yHcH},
	"^DB""+Adj""+AorPart" -> {+Hr},
	"^DB""+Adj""+FeelLike" -> {+yAsH},
	"^DB""+Adj""+FutPart""+Pnon" -> {+yAcAk},
	"^DB""+Adj""+NarrPart" -> {+mHş},
	"^DB""+Adj""+PastPart""+Pnon" -> {+DHk},
	"^DB""+Adj""+PresPart" -> {+yAn}
];

define DefaultVerbMorphemes2 [
	"^DB""+Verb""+Able""+Neg" -> {+yAmA},
	"^DB""+Noun""+ActOf""+A3sg""+Pnon""+Nom" -> {+mAcA},
	"^DB""+Noun""+Agt""+A3sg""+Pnon""+Nom" -> {+yHcH},
	"^DB""+Noun""+FeelLike""+A3sg""+Pnon""+Nom" -> {+yAsH},
	"^DB""+Noun""+FutPart""+A3sg""+P3sg""+Nom" -> {+yAcAğH},
	"^DB""+Noun""+Inf1""+A3sg""+Pnon""+Nom" -> {+mAk},
	"^DB""+Noun""+Inf2""+A3sg""+Pnon""+Nom" -> {+mA},
	"^DB""+Noun""+Inf3""+A3sg""+Pnon""+Nom" -> {+yHş},
	"^DB""+Noun""+NotAbleState""+A3sg""+Pnon""+Nom" -> {+yAmAzlHk},
	"^DB""+Noun""+NotState""+A3sg""+Pnon""+Nom" -> {+mAzlHk},
	"^DB""+Noun""+PastPart""+A3sg""+P3sg""+Nom" -> {+DHğH}
];

define PDC ["+Past"|"+Past2"|"+Desr"|"+Cond"];
# define Tense ["+Aor"|"+Desr"|"+Fut"|"+Narr"|"+Neces"|"+Opt"|"+Past"|"+Past2"|"+Prog1"|"+Prog2"];
define AorAr [{affet}|{ak}|{aktet}|{an}|{art}|{as}|{aş}|{at}|{ay}|{az}|{bahset}|{bak}|{bas}|{bat}|{biç}|{bin}|{bit}|{boğ}|{boz}|{böl}|{coş}|{çak}|{çal}|{çarp}|{çek}|{çık}|{çırp}|{çiz}|{çöz}|{değ}|{dik}|{dil}|{din}|{doğ}|{dol}|{don}|{dök}|{dön}|{döv}|{duy}|{düş}|{eğ}|{ek}|{em}|{emret}|{er}|{es}|{et}|{ez}|{farket}|{fethet}|{geç}|{gez}|{gir}|{git}|{giy}|{göç}|{gül}|{hallet}|{hapset}|{hisset}|{iç}|{in}|{it}|{kaç}|{kalk}|{kan}|{kap}|{kar}|{kat}|{katet}|{katlet}|{kay}|{kaybet}|{kes}|{keşfet}|{kıl}|{kır}|{kırp}|{kıy}|{kız}|{kon}|{kop}|{kork}|{koş}|{kov}|{koy}|{kur}|{kus}|{ov}|{ölç}|{öp}|{ör}|{ört}|{öv}|{reddet}|{saç}|{sar}|{sark}|{sars}|{sat}|{say}|{seç}|{sev}|{seyret}|{sez}|{sığ}|{sil}|{sok}|{sor}|{sök}|{sön}|{söv}|{sözet}|{sun}|{sür}|{sürt}|{süz}|{şaş}|{tak}|{taş}|{terket}|{tut}|{uç}|{um}|{uy}|{üz}|{yağ}|{yak}|{yan}|{yap}|{yar}|{yat}|{yaz}|{yen}|{yet}|{yık}|{yor}|{yut}|{yüz}|{zannet}];
define CausAr [{çık}|{çök}|{kop}];
define CausHr [{art}|{aş}|{bat}|{bit}|{doğ}|{doy}|{düş}|{duy}|{geç}|{iç}|{kaç}|{piş}|{şiş}|{taş}|{uç}|{vazgeç}|{yat}|{yit}];
define CausHt [{ak}|{çarp}|{kork}|{ürk}];
define Consonant [b|c|ç|d|f|g|ğ|h|j|k|l|m|n|p|r|s|ş|t|v|y|z];
define Vowel [a|e|ı|i|o|ö|u|ü];

define AlternativeMorphemes1 [
	"+A1pl" -> {+k}		|| PDC _ ,,
	"+A1pl" -> {+lHm}	|| "+Opt" _ ,,
	"+A1pl" -> {+yHz}	|| "+Neg""+Aor" _ ,,
	"+A1sg" -> {+m}		|| [PDC | "+Neg""+Aor"] _ ,,
	"+A2pl" -> {+nHz}	|| PDC _ ,,
	"+A2pl" -> {+nHzA}	|| "+Imp2" _ ,,
	"+A2pl" -> {+yHn}	|| "+Imp" _ ,,
	"+A2pl" -> {+yHnHz}	|| "+Imp" _ ,,
	"+A2sg" -> {+n}		|| PDC _ ,,
	"+A2sg" -> {+nA}	|| "+Imp2" _ ,,
	"+A2sg" -> 0		|| "+Imp" _ ,,
	"+A3pl" -> {+sHnlAr}	|| "+Imp" _ ,,
	"+A3sg" -> {+sHn}	|| "+Imp" _ ,,
 	"+Aor" -> 0		|| "+Neg" _ ["+A1sg"|"+A1pl"] ,,
 	"+Aor" -> {+z}		|| "+Neg" _ \["+A1sg"|"+A1pl"] ,,
	"^DB""+Adj""+AorPart" -> {+z} || "+Neg" _
];

define AlternativeMorphemes4 [
 	{+Hr} -> {+Ar}	// .#. AorAr _ .o.
	{+Hl} -> {+Hn}	// [Vowel|l] _ .o.
 	{+t} -> {+DHr}	// .#. Consonant* Vowel Consonant* _ .o.
 	{+t} -> {+DHr}	// \[Vowel|r|l] _ .o.
 	{+t} -> {+Ar}	// .#. CausAr _ .o.
 	{+t} -> {+Hr}	// .#. CausHr _ .o.
 	{+t} -> {+Ht}	// .#. CausHt _
];

define verbMorphemes [
	VerbRoot0 .o.
	AlternativeMorphemes1 .o.
	DefaultVerbMorphemes2 .o.
	DefaultVerbMorphemes3 .o.
	AlternativeMorphemes4
];

push verbMorphemes
