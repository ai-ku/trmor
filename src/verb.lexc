! Turkish Verb Morphotactics: please see the end of the file for documentation

Multichar_Symbols
	+Abl +Able +Acc +Acquire +ActOf +Adamantly +Adj +Adverb
	+AfterDoingSo +Agt +Almost +Aor +AorPart +AsIf +AsLongAs +A1pl
	+A1sg +A2pl +A2sg +A3pl +A3sg +Become +ByDoingSo +Card +Caus
	+Cond +Conj +Continue +Cop +Dat +Demons +Desr +Det +Dim +Dist
	+Dup +Equ +EverSince +FeelLike +Fut +FutPart +Gen +Hastily +Imp
	+InBetween +Inf1 +Inf2 +Inf3 +Ins +Interj +JustLike +Loc +Ly
	+Narr +NarrPart +Neces +Neg +Ness +Nom +NotAbleState +NotState
	+Noun +Num +Opt +Ord +PCAbl +PCAcc +PCDat +PCGen +PCIns +PCNom
	+Pass +Past +PastPart +Pers +Pnon +Pos +Postp +Pres +PresPart
	+Prog1 +Prog2 +Pron +Prop +P1pl +P1sg +P2pl +P2sg +P3pl +P3sg
	+Quant +Ques +Recip +Reflex +Rel +Related +Repeat +Since
	+SinceDoingSo +Start +Stay +Verb +When +While +With +Without
	+WithoutBeingAbleToHaveDoneSo +WithoutHavingDoneSo +Zero ^DB

LEXICON Root
!+Verb:0	Voice;
+Verb+Pos:yap	Tense;

LEXICON Voice
			Polarity;
^DB+Verb+Pass:+Hl	Polarity;
^DB+Verb+Caus:+t	Voice3;
^DB+Verb+Recip:+Hş	Voice2;
^DB+Verb+Reflex:+Hn	Voice2;

LEXICON Voice3
			Polarity;
^DB+Verb+Pass:+Hl	Polarity;
^DB+Verb+Caus:+t	Voice2;

LEXICON Voice2
			Polarity;
^DB+Verb+Pass:+Hl	Polarity;
^DB+Verb+Caus:+t	Voice1;

LEXICON Voice1
			Polarity;
^DB+Verb+Pass:+Hl	Polarity;

LEXICON Polarity
+Pos:0			Compound;
+Neg:+mA		Compound;
^DB+Verb+Able+Neg:+yAmA	Compound;

LEXICON Compound
				Deriv;
^DB+Verb+Able:+yAbil		Deriv;
^DB+Verb+Almost:+yAyaz		Deriv;
^DB+Verb+Continue:+yAgit	Deriv;
^DB+Verb+EverSince:+yAgel	Deriv;
^DB+Verb+Hastily:+yHver		Deriv;
^DB+Verb+Repeat:+yAdur		Deriv;
^DB+Verb+Start:+yAkoy		Deriv;
^DB+Verb+Stay:+yAkal		Deriv;

LEXICON Deriv
			Tense;
^DB+Adj+Agt:+yHcH	#;
^DB+Adj+AorPart:+Hr	#;
^DB+Adj+FeelLike:+yAsH	#;
^DB+Adj+FutPart:+yAcAk	#;
^DB+Adj+NarrPart:+mHş	#;
^DB+Adj+PastPart:+DHk	#;
^DB+Adj+PresPart:+yAn	#;
!^DB+Adj+Zero:+z		#;  ! what is this?
^DB+Adverb+Adamantly:+yAsHyA	#;
^DB+Adverb+AfterDoingSo:+yHp	#;
^DB+Adverb+AsLongAs:+DHkCA	#;
^DB+Adverb+ByDoingSo:+yArAk	#;
^DB+Adverb+SinceDoingSo:+yAlH	#;
^DB+Adverb+When:+yHncA	#;
^DB+Adverb+WithoutBeingAbleToHaveDoneSo:+yAmAdAn	#;
^DB+Adverb+WithoutHavingDoneSo:+mAdAn	#;
^DB+Adverb+WithoutHavingDoneSo:+mAksHzHn	#;
^DB+Noun+ActOf:+mAcA	#;
^DB+Noun+Agt:+yHcH	#;
^DB+Noun+FeelLike:+yAsH	#;
^DB+Noun+FutPart:+yAcAk	#;
^DB+Noun+Inf1:+mAk	#;
^DB+Noun+Inf2:+mA	#;
^DB+Noun+Inf3:+yHş	#;
^DB+Noun+NotAbleState:+yAmAzlHk	#;
^DB+Noun+NotState:+mAzlHk	#;
^DB+Noun+PastPart:+DHk	#;

LEXICON Tense
+Aor:+Hr	CNP|Person;
+Aor:+Hr	AorAdv;
+Desr:+sA	NP|Person2;
+Fut:+yAcAk	CNP|Person+Cop;
+Imp:0		PersonImp;
+Imp:+sA	PersonImpSA;
+Narr:+mHş	CNP|Person+Cop;
+Narr:+mHş	NarrAdv1;
+Neces:+mAlH	CNP|Person+Cop;
+Opt:+yA	NP|Person1;
+Past:+DH	CP|Person;
+Prog1:+Hyor	CNP|Person+Cop;
+Prog2:+mAktA	CNP|Person+Cop;

LEXICON CNP|Person+Cop
		CNP+Person;
		Person1+Cop?;

LEXICON CNP|Person
		CNP+Person;
		Person1;

LEXICON CP|Person
		C+Person;
		Person2+CP?;

LEXICON NP|Person1
		NP+Person;
		Person3;    !!! Only difference with Person1 is A1pl

LEXICON NP|Person2
		NP+Person;
		Person2;

LEXICON CNP+Person
+A3pl:+lAr	CNP;
+Cond:+ysA	Person2;
+Past:+yDH	Person2;
+Narr:+ymHş	Person1+Cop?;
+Narr:+ymHş	NarrAdv2;
		While;

LEXICON CNP
+Cond:+ysA	#;
+Narr:+ymHş	#;
+Past:+yDH	#;
		While;

LEXICON NP+Person
+A3pl:+lAr	NP;
+Narr:+ymHş	Person1;
+Past:+yDH	Person2;

LEXICON NP
+Narr:+ymHş	#;
+Past:+yDH	#;

LEXICON C+Person
+Cond:+ysA	Person2;

LEXICON Person1
+A1sg:+yHm	#;
+A2sg:+sHn	#;
+A3sg:0		#;
+A1pl:+yHz	#;
+A2pl:+sHnHz	#;
+A3pl:+lAr	#;

LEXICON Person1+Cop?
+A1sg:+yHm	Cop?;
+A2sg:+sHn	Cop?;
+A3sg:0		Cop?;
+A1pl:+yHz	Cop?;
+A2pl:+sHnHz	Cop?;
+A3pl:+lAr	Cop?;

LEXICON Person2
+A1sg:+m  	#;
+A2sg:+n  	#;
+A3sg:0	  	#;
+A1pl:+k  	#;
+A2pl:+nHz	#;
+A3pl:+lAr	#;

LEXICON Person2+CP?
+A1sg:+m  	CP?;
+A2sg:+n  	CP?;
+A3sg:0	  	CP?;
+A1pl:+k  	CP?;
+A2pl:+nHz	CP?;
+A3pl:+lAr	CP?;

LEXICON Person3
+A1sg:+yHm	#;
+A2sg:+sHn	#;
+A3sg:0		#;
+A1pl:+lHm	#;
+A2pl:+sHnHz	#;
+A3pl:+lAr	#;

LEXICON PersonImp
+A2sg:0		#;
+A3sg:+sHn	#;
+A2pl:+yHn	#;
+A2pl:+yHnHz	#;
+A3pl:+sHnlAr	#;

LEXICON PersonImpSA
+A2sg:+nA	#;
+A2pl:+nHzA	#;

LEXICON NarrAdv1
+A3sg:0		AsIf;
+A3pl:+lAr	AsIf;

LEXICON NarrAdv2
+A3sg:0		AsIf;
+A3pl:+lAr	While;

LEXICON AorAdv
+A3sg:0		AsIf;

LEXICON While
^DB+Adverb+While:+yken	#;

LEXICON AsIf
^DB+Adverb+AsIf:+CAsHnA	#;

LEXICON Cop?
+Cop:+DHr	#;
		#;

LEXICON CP?
+Cond:+ysA	#;
+Past:+yDH	#;
		#;

END

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; General description

To a first approximation verb morphology consists of five sections:
voice, polarity, compounding, tense, person.  Voice and compounding
are optional.  There may also be a derivational suffix changing part
of speech following any of the above ???

Voice: +Caus +Pass +Recip +Reflex
Ordering: +Pass | +Reflex | (+Caus|+Recip)(+Caus)*(+Pass)?

Polarity: +Pos +Neg:mA ^DB+Verb+Able+Neg:yAmA

Compounding: At most one observed. Following ^DB+Verb.
+Able:
+Almost:
+EverSince:
+Hastily:
+Repeat:
+Start:
+Stay:

Tense: +Aor +Desr +Fut +Imp +Narr +Neces +Opt +Past +Prog1 +Prog2
       +Cond +Cop
Ordering: 
(+Aor|+Fut|+Narr|+Neces|+Prog1|+Prog2)? (+Narr)? (+Cop? | +Past? +Cond?)
(+Desr) (+Narr|+Past)?
(+Imp)
(+Opt)


;;;; Issues

;;; Issue 1: will change the handling of degil:
;;; +verb+neg+pres only seen with degil.

değil	değil+Verb+Neg+Pres+A3sg
Değil	değil+Verb+Neg+Pres+A3sg
DEĞİL	değil+Verb+Neg+Pres+A3sg
değildi	değil+Verb+Neg+Past+A3sg
değildik	değil+Verb+Neg+Past+A1pl
değildir	değil+Verb+Neg+Pres+A3sg+Cop
Değildir	değil+Verb+Neg+Pres+A3sg+Cop
değilim	değil+Verb+Neg+Pres+A1sg
DEĞİLİM	değil+Verb+Neg+Pres+A1sg
değiliz	değil+Verb+Neg+Pres+A1pl
DEĞİLİZ	değil+Verb+Neg+Pres+A1pl
değiller	değil+Verb+Neg+Pres+A3pl
değillerdi	değil+Verb+Neg+A3pl+Past
değillerdir	değil+Verb+Neg+Pres+A3pl+Cop
değillermiş	değil+Verb+Neg+A3pl+Narr
değillerse	değil+Verb+Neg+A3pl+Cond
değilmiş	değil+Verb+Neg+Narr+A3sg
değilse	değil+Verb+Neg+Cond+A3sg
Değilse	değil+Verb+Neg+Cond+A3sg
değilseniz	değil+Verb+Neg+Cond+A2pl
değilsin	değil+Verb+Neg+Pres+A2sg
değilsiniz	değil+Verb+Neg+Pres+A2pl


;;; Issue 2: Will change the handling of ise etc.

idi	i	+Verb+Pos+Past+A3sg
idiler	i	+Verb+Pos+Past+A3pl
idim	i	+Verb+Pos+Past+A1sg
İdim	i	+Verb+Pos+Past+A1sg
idiyseler	i	+Verb+Pos+Past+Cond+A3pl
imişçesine	i	+Verb+Pos+Narr+A3sg^DB+Adverb+AsIf
imiş	i	+Verb+Pos+Narr+A3sg
imişler	i	+Verb+Pos+Narr+A3pl
imişsin	i	+Verb+Pos+Narr+A2sg
ise	i	+Verb+Pos+Cond+A3sg
isek	i	+Verb+Pos+Cond+A1pl
iseler	i	+Verb+Pos+Cond+A3pl
isen	i	+Verb+Pos+Cond+A2sg
İsen	i	+Verb+Pos+Cond+A2sg


;;; Issue 3: upper ambiguities: most common suffixes listed first.
;;; We will deal with this with a regexp layer.

Aor: +Hr +Ar +r -- See below
Cond: +ysA +sA -- +sA form only used for "ise"
Caus: +dHr +Ar +Hr +Ht +t
Pass: +Hl +Hn +HnHl
Imp:  0, +sA -- +sA form for istesenize, baksana vs.
Narr: +mHS +ymHS -- +ymHS form only as Tense2 or derived verb
Past: +dH +ydH -- +ydH form only as Tense2 or derived verb

;; Caus: Parens show type/token frequency.
+Ar (3/1455): çık, çök, kop  -- çöker does not sound right?
+Hr (18/2222): art, aş, bat, bit, doğ, doy, düş, duy, geç, iç, kaç, piş, şiş, taş, uç, vazgeç, yat, yit
+Ht (4/134): ak, çarp, kork, ürk
+t (170/7992): multi-syllable, ends in vowel or [rl].
+dHr (442/17974): all the rest.

;; Pass:
+HnHl (20/718): Allow for double passives?  istenilmek etc.
	These are better represented as reflex+passives.
+Hn (340/16573): ends in vowel, or L.
+Hl (542/29198): the rest.

;; Cond, Narr, Past: the +y form used in Tense2 or derived position
only.  Compare "acisa, acimis, acidi" vs. "aciysa, aciymis, aciydi".
In fact these should have a different name Cond2, Narr2, Past2 etc.

;; Imp, Opt, Desr: Similarly (+sA)+Imp should have a different name.
We should carefully compare Opt, Desr, and two forms of Imp.

(+yA)+Opt(+yHm)+A1sg
(+yA)+Opt(+sHn)+A2sg
(+yA)+Opt+A3sg
(+yA)+Opt(+lHm)+A1pl
(+yA)+Opt(+sHnHz)+A2pl
(+yA)+Opt(+lAr)+A3pl

(+yA)+Opt(+lAr)+A3pl(+ydH)+Past
(+yA)+Opt(+ydH)+Past+A3sg
(+yA)+Opt(+ydH)+Past(+n)+A2sg

+Imp+A2sg
+Imp(+sHn)+A3sg
+Imp(+yHn)+A2pl
+Imp(+yHnHz)+A2pl
+Imp(+sHnlAr)+A3pl

(+sA)+Imp(+nA)+A2sg
(+sA)+Imp(+nHzA)+A2pl

(+sA)+Desr(+m)+A1sg
(+sA)+Desr(+n)+A2sg
(+sA)+Desr+A3sg
(+sA)+Desr(+k)+A1pl
(+sA)+Desr(+nHz)+A2pl
(+sA)+Desr(+lAr)+A3pl

(+sA)+Desr(+lAr)+A3pl(+ydH)+Past
(+sA)+Desr(+ydH)+Past+A3sg
(+sA)+Desr(+ydH)+Past(+k)+A1pl
(+sA)+Desr(+ydH)+Past(+lAr)+A3pl
(+sA)+Desr(+ydH)+Past(+m)+A1sg
(+sA)+Desr(+ydH)+Past(+n)+A2sg
(+sA)+Desr(+ydH)+Past(+nHz)+A2pl
(+sA)+Desr(+ymHS)+Narr+A3sg

;; Aor: 3 forms roughly equal in frequency.  +r after vowels.  +Ar
seems to only follow single syllable words and compound verbs that end
with "et".  Everything else takes -Hr (unfortunately some single
syllable words and non-compound "et" endings are in this list).  Here
are the lists from the training file:

; +Ar (141): affet ak aktet an art as aş at ay az bahset bak bas bat biç
bin bit boğ boz böl coş çak çal çarp çek çık çırp çiz çöz değ dik dil
din doğ dol don dök dön döv duy düş eğ ek em emret er es et ez farket
fethet geç gez gir git giy göç gül hallet hapset hisset iç in it kaç
kalk kan kap kar kat katet katlet kay kaybet kes keşfet kıl kır kırp
kıy kız kon kop kork koş kov koy kur kus ov ölç öp ör ört öv reddet
saç sar sark sars sat say seç sev seyret sez sığ sil sok sor sök sön
söv sözet sun sür sürt süz şaş tak taş terket tut uç um uy üz yağ yak
yan yap yar yat yaz yen yet yık yor yut yüz zannet

; +Hr (177): 18 exceptions marked with parens:
abart ağar aktar (al) aldan alın alış anlaş anlat araştır aydınlat
ayır ay$ır azal bağır bakın başar başvur bayıl beğen bırak (bil)
birleş (bul) bulun buluş buyrul buyur buy$ur büyüt çalış çevir çev$ir
çıkış çırpın çoğal çözün dağıl dağıt daral darıl davran dayan değin
değiş deyiver didin dikil dinlen diren diril dokun dolaş dönüş (dur)
duyur düşün düzel edin eğit eğlen eleştir eriş evlen (gel) geliş gerek
gereksin getir gezin gider giriş gönder (gör) görün görüş göster götür
(gözet) gücen güven hapşır hastalan havalan hesaplaş hoşgör hoşlan
ısın ısır içer içlen (ilet) ilgilen inan incin irkil kabullen kaçın
kahrol (kal) kaldır kalkış kapış karar karış karşılaş katlan kavuş
kaybol kaydol kazan kınan kıskan kıvran kızış konuş kullan kurtar
kurtul kuşat malol mırıldan (ol) oluş otur öğren (öğret) (öl) öner
öngör övün özen paylaş (san) savaş savun seslen sevin seviş sıkıl
soruştur sömür söyleş şaşır takıl tanış tartış tırman tokalaş (tüket)
uğraş ulaş unut utan uyan uyar uzlaş üstlen (var) (ver) (vur) vurul
yakın yakış yaklaş yapış yaraş yarat yarış yeral yerleş yıprat yoğur
yoğ$ur yönel (yönet) yüksel

;;; Issue 4: lower ambiguities

+Ar: Aor Caus
+dHr: Cop Caus
+Hn: A2sg Pass Reflex
+Hr: (Aor) Caus
+mA: Neg Inf2
+sA: Cond Desr Imp
+sHn: A2sg A3sg
+z: A3sg A3pl

;;; Issue 5: different person suffixes 
;;; (= means default, - means not applicable)

Default:	Aor/Narr:mHS	NegAor:		Desr/Past:
+A1sg:+yHm	+A1sg:+Hm	+A1sg:+m     	+A1sg:+m  
+A2sg:+sHn	+A2sg:=		+A2sg:+zsHn  	+A2sg:+n  
+A3sg:0		+A3sg:=		+A3sg:+z     	+A3sg:=	  
+A1pl:+yHz	+A1pl:+Hz	+A1pl:+yHz   	+A1pl:+k  
+A2pl:+sHnHz	+A2pl:=		+A2pl:+zsHnHz	+A2pl:+nHz
+A3pl:+lAr	+A3pl:=		+A3pl:+zlAr  	+A3pl:+lAr

Imp:		Imp:sA		Opt:	  
+A1sg:-		+A1sg:-		+A1sg:=
+A2sg:0	      	+A2sg:+nA	+A2sg:=	  
+A3sg:+sHn	+A3sg:-		+A3sg:=	  
+A1pl:-	      	+A1pl:-		+A1pl:+lHm
+A2pl:+yHn(Hz)	+A2pl:+nHzA	+A2pl:=	  
+A3pl:+sHnlAr 	+A3pl:-		+A3pl:=   

Other tenses take default suffixes.  Exceptions:
- Narr:mHS+A2sg can also be +Hn (otherwise same as Aor)
- Narr:ymHS+A1pl can also be +Hz (otherwise default)
- Aor/Narr can probably be handled by regex.
- NegAor should use the z suffix, then A1 fixed by regex.

;;; Issue 6: different sets of Tense2 allowed.  By default all Tenses
take Narr, Past, Cond, Cop as second Tense.  Exceptions:

+Aor:+Hr	Cop cannot immediately follow.
+Desr:+sA	Cond/Cop cannot follow.
+Imp:0		Nothing can follow.
+Narr:+mHş	Second Narr rare but observed.
+Opt:+yA	Only Past observed.
+Past:+dH	No Cop/Narr.  Second Past rare.

;;; Issue 7: needless proliferation of ambiguity should be handled by
systematic noun adjective conversion:

^DB+Adj+Agt:+yHcH	#;   ! don't need both noun and adj
^DB+Adj+FeelLike:+yAsH	#;   ! don't need both noun and adj
^DB+Adj+FutPart:+yAcAk	#;   ! don't need both noun and adj
^DB+Adj+PastPart:+DHk	#;   ! don't need both noun and adj
^DB+Noun+Agt:+yHcH	#;   ! don't need both noun and adj
^DB+Noun+FeelLike:+yAsH	#;   ! don't need both noun and adj
^DB+Noun+FutPart:+yAcAk	#;   ! don't need both noun and adj
^DB+Noun+PastPart:+DHk	#;   ! don't need both noun and adj


;;;; Test 090902-09:44: 

The following 21 sequences following Pos/Neg from Milliyet are not
covered by this grammar:

+Aor+Past+A3pl+Cond: 
	çıkardılarsa - this is the wrong interpretation, correct one
	is +Verb^DB+Verb+Caus+Pos+Past+A3pl+Cond 

Similarly this should not be covered:
+Verb+Pos+Aor+Narr^DB+Adverb+While
1	çıkarmışken	çık	+Verb+Pos+Aor+Narr^DB+Adverb+While

^DB+Verb+Continue^DB+Adj+PresPart:
	süregiden	sür	+Verb+Pos^DB+Verb+Continue^DB+Adj+PresPart
	Added to the Compound grammar.

The following are only seen after degil+Verb+Neg:
^DB+Adverb+While +A3pl+Tense2 +Pres+Person

The following are seen after degil+Verb+Neg or i+Verb+Pos:
+Cond+Person


;;;; Test 090903-11:39

The following do not need to be covered (cikar is the root):

+Verb+Pos+Aor+Narr^DB+Adverb+While
çıkarmışken	çık	+Verb+Pos+Aor+Narr^DB+Adverb+While

+Verb+Pos+Aor+Past+A3pl+Cond
çıkardılarsa	çık	+Verb+Pos+Aor+Past+A3pl+Cond

All other postneg entries from test (except ^DB entries) are covered.

verb.lexc (postneg) has 326 paths.
upper net has 323 paths.
Milliyet.postneg has 531 paths.
extra = [vupper - test] has 93 paths.
All but "yapaydi" is recognized by tfeaturesulx.

