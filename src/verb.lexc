! Turkish Verb Morphotactics: please see end of file for documentation

Multichar_Symbols
	+Abl +Able +Acc +Acquire +ActOf +Adamantly +Adj +Adverb
	+AfterDoingSo +Agt +Almost +Aor +AorPart +AsIf +AsLongAs +A1pl
	+A1sg +A2pl +A2sg +A3pl +A3sg +Become +ByDoingSo +Card +Caus
	+Cond +Conj +Continue +Cop +Dat +Demons +Desr +Det +Dim +Dist
	+Dup +Equ +EverSince +FeelLike +Fut +FutPart +Gen +Hastily +Imp
	+InBetween +Inf1 +Inf2 +Inf3 +Ins +Interj +JustLike +Loc +Ly
	+Narr +NarrPart +Neces +Neg +Ness +Nom +NotAbleState +NotState
	+Noun +Num +Opt +Ord +PCAbl +PCAcc +PCDat +PCGen +PCIns +PCNom
	+Pass +Past +PastPart +Pers +Pnon +Pos +Postp +Pres +PresPart
	+Prog1 +Prog2 +Pron +Prop +P1pl +P1sg +P2pl +P2sg +P3pl +P3sg
	+Quant +Ques +Recip +Reflex +Rel +Related +Repeat +Since
	+SinceDoingSo +Start +Stay +Verb +When +While +With +Without
	+WithoutBeingAbleToHaveDoneSo +WithoutHavingDoneSo +Zero ^DB


LEXICON Root
+Verb	Voice;

LEXICON Voice
			Polarity;
^DB+Verb+Caus:+Ar	Caus2;
^DB+Verb+Recip:+Hş	Caus2;
^DB+Verb+Pass:+Hl	Polarity;
^DB+Verb+Reflex:+Hn	Polarity;

LEXICON Caus2
^DB+Verb+Caus:+Ar	Caus3;
^DB+Verb+Pass:+Hl	Polarity;
			Polarity;

LEXICON Caus3
^DB+Verb+Caus:+Ar	Polarity;
^DB+Verb+Pass:+Hl	Polarity;
			Polarity;

LEXICON Polarity
+Pos		Compound;
+Neg		Compound;
^DB+Verb+Able+Neg	Compound;

LEXICON Compound
			Deriv;
^DB+Verb+Able		Deriv;
^DB+Verb+Almost		Deriv;
^DB+Verb+EverSince	Deriv;
^DB+Verb+Hastily	Deriv;
^DB+Verb+Repeat		Deriv;
^DB+Verb+Start		Deriv;
^DB+Verb+Stay		Deriv;

LEXICON Deriv
			Tense;
^DB+Adj+Agt:+yHcH	#;
^DB+Adj+AorPart:+Ar	#;
^DB+Adj+FeelLike:+yAsH	#;
^DB+Adj+FutPart:+yAcAk	#;
^DB+Adj+NarrPart:+mHş	#;
^DB+Adj+PastPart:+dHk	#;
^DB+Adj+PresPart:+yAn	#;
^DB+Adj+Zero:+z	#;
^DB+Adverb+Adamantly:+yAsHyA	#;
^DB+Adverb+AfterDoingSo:+yHp	#;
^DB+Adverb+AsLongAs:+dHkCA	#;
^DB+Adverb+ByDoingSo:+yArAk	#;
^DB+Adverb+SinceDoingSo:+yAlH	#;
^DB+Adverb+When:+yHncA	#;
^DB+Adverb+WithoutBeingAbleToHaveDoneSo:+yAmAdAn	#;
^DB+Adverb+WithoutHavingDoneSo:+mAdAn	#;
^DB+Adverb+WithoutHavingDoneSo:+mAksHzHn	#;
^DB+Noun+ActOf:+mAcA	#;
^DB+Noun+Agt:+yHcH	#;
^DB+Noun+FeelLike:+yAsH	#;
^DB+Noun+FutPart:+yAcAk	#;
^DB+Noun+Inf1:+mAk	#;
^DB+Noun+Inf2:+mA	#;
^DB+Noun+Inf3:+yHS	#;
^DB+Noun+NotAbleState:+yAmAzlHk	#;
^DB+Noun+NotState:+mAzlHk	#;
^DB+Noun+PastPart:+dHk	#;

LEXICON Tense
+Aor		A3pl;
+Desr		A3pl;
+Fut		A3pl;
+Imp		ImpP;
+Narr		A3pl;
+Neces		A3pl;
+Opt		A3pl;
+Past		PastP;
+Prog1		A3pl;
+Prog2		A3pl;

LEXICON A3pl
+A3pl	Narr;
	Narr;

LEXICON Narr
+Narr	While;
	While;

LEXICON While
^DB+Adverb+While:+yken	#;
	Past;

LEXICON Past
+Past	PastP;
	Cond;

LEXICON Cond
+Cond	Person;
	Person;

LEXICON AsIf
^DB+Adverb+AsIf:+cAsHnA	#;
	Cop;

LEXICON Cop
+Cop	#;
	#;

LEXICON Person
	AsIf;
+A1sg	AsIf;
+A2sg	AsIf;
+A3sg	AsIf;
+A1pl	AsIf;
+A2pl	AsIf;
+A3pl	AsIf;

LEXICON ImpP
+A1sg	#;
+A2sg	#;
+A3sg	#;
+A1pl	#;
+A2pl	#;
+A3pl	#;

LEXICON PastP
	Cond;
+A1sg	Cond;
+A2sg	Cond;
+A3sg	Cond;
+A1pl	Cond;
+A2pl	Cond;
+A3pl	Cond;

END

;;; General description

To a first approximation verb morphology consists of four sections:
derivation, negation, tense, person.

Derivation: +Caus +Pass +Recip +Reflex
Ordering: +Pass | +Reflex | (+Caus|+Recip)(+Caus)*(+Pass)?

Tense: +Aor +Desr +Fut +Imp +Narr +Neces +Opt +Past +Prog1 +Prog2
       +Cond +Cop
Ordering: 
(+Aor|+Fut|+Narr|+Neces|+Prog1|+Prog2)? (+Narr)? (+Cop? | +Past? +Cond?)
(+Desr) (+Narr|+Past)?
(+Imp)
(+Opt)


;;; Issues

;; Issue 1: will change the handling of degil:
;; +verb+neg+pres only seen with degil.

değil	değil+Verb+Neg+Pres+A3sg
Değil	değil+Verb+Neg+Pres+A3sg
DEĞİL	değil+Verb+Neg+Pres+A3sg
değildi	değil+Verb+Neg+Past+A3sg
değildik	değil+Verb+Neg+Past+A1pl
değildir	değil+Verb+Neg+Pres+A3sg+Cop
Değildir	değil+Verb+Neg+Pres+A3sg+Cop
değilim	değil+Verb+Neg+Pres+A1sg
DEĞİLİM	değil+Verb+Neg+Pres+A1sg
değiliz	değil+Verb+Neg+Pres+A1pl
DEĞİLİZ	değil+Verb+Neg+Pres+A1pl
değiller	değil+Verb+Neg+Pres+A3pl
değillerdi	değil+Verb+Neg+A3pl+Past
değillerdir	değil+Verb+Neg+Pres+A3pl+Cop
değillermiş	değil+Verb+Neg+A3pl+Narr
değillerse	değil+Verb+Neg+A3pl+Cond
değilmiş	değil+Verb+Neg+Narr+A3sg
değilse	değil+Verb+Neg+Cond+A3sg
Değilse	değil+Verb+Neg+Cond+A3sg
değilseniz	değil+Verb+Neg+Cond+A2pl
değilsin	değil+Verb+Neg+Pres+A2sg
değilsiniz	değil+Verb+Neg+Pres+A2pl


;; Issue 2: Will change the handling of ise etc.

idi	i	+Verb+Pos+Past+A3sg
idiler	i	+Verb+Pos+Past+A3pl
idim	i	+Verb+Pos+Past+A1sg
İdim	i	+Verb+Pos+Past+A1sg
idiyseler	i	+Verb+Pos+Past+Cond+A3pl
imişçesine	i	+Verb+Pos+Narr+A3sg^DB+Adverb+AsIf
imiş	i	+Verb+Pos+Narr+A3sg
imişler	i	+Verb+Pos+Narr+A3pl
imişsin	i	+Verb+Pos+Narr+A2sg
ise	i	+Verb+Pos+Cond+A3sg
isek	i	+Verb+Pos+Cond+A1pl
iseler	i	+Verb+Pos+Cond+A3pl
isen	i	+Verb+Pos+Cond+A2sg
İsen	i	+Verb+Pos+Cond+A2sg
