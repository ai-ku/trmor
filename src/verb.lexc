! Turkish Verb Morphotactics: please see end of file for documentation

Multichar_Symbols
	+Abl +Able +Acc +Acquire +ActOf +Adamantly +Adj +Adverb
	+AfterDoingSo +Agt +Almost +Aor +AorPart +AsIf +AsLongAs +A1pl
	+A1sg +A2pl +A2sg +A3pl +A3sg +Become +ByDoingSo +Card +Caus
	+Cond +Conj +Continue +Cop +Dat +Demons +Desr +Det +Dim +Dist
	+Dup +Equ +EverSince +FeelLike +Fut +FutPart +Gen +Hastily +Imp
	+InBetween +Inf1 +Inf2 +Inf3 +Ins +Interj +JustLike +Loc +Ly
	+Narr +NarrPart +Neces +Neg +Ness +Nom +NotAbleState +NotState
	+Noun +Num +Opt +Ord +PCAbl +PCAcc +PCDat +PCGen +PCIns +PCNom
	+Pass +Past +PastPart +Pers +Pnon +Pos +Postp +Pres +PresPart
	+Prog1 +Prog2 +Pron +Prop +P1pl +P1sg +P2pl +P2sg +P3pl +P3sg
	+Quant +Ques +Recip +Reflex +Rel +Related +Repeat +Since
	+SinceDoingSo +Start +Stay +Verb +When +While +With +Without
	+WithoutBeingAbleToHaveDoneSo +WithoutHavingDoneSo +Zero ^DB

LEXICON Root
+Verb:0	Voice;

LEXICON Voice
			Polarity;
^DB+Verb+Caus:+t	Voice2;
^DB+Verb+Recip:+Hş	Voice2;
^DB+Verb+Pass:+Hl	Polarity;
^DB+Verb+Reflex:+Hn	Polarity;

LEXICON Voice2
			Polarity;
^DB+Verb+Pass:+Hl	Polarity;
^DB+Verb+Caus:+t	Voice3;

LEXICON Voice3
			Polarity;
^DB+Verb+Caus:+t	Polarity;
^DB+Verb+Pass:+Hl	Polarity;

LEXICON Polarity
+Pos:0			Compound;
+Neg:+mA		Compound;
^DB+Verb+Able+Neg:+yAmA	Compound;

LEXICON Compound
				Deriv;
^DB+Verb+Able:+yAbil		Deriv;
^DB+Verb+Almost:+yAyaz		Deriv;
^DB+Verb+EverSince:+yAgel	Deriv;
^DB+Verb+Hastily:+yHver		Deriv;
^DB+Verb+Repeat:+yAdur		Deriv;
^DB+Verb+Start:+yAkoy		Deriv;
^DB+Verb+Stay:+yAkal		Deriv;

LEXICON Deriv
			Tense;
^DB+Adj+Agt:+yHcH	#;
^DB+Adj+AorPart:+Ar	#;
^DB+Adj+FeelLike:+yAsH	#;
^DB+Adj+FutPart:+yAcAk	#;
^DB+Adj+NarrPart:+mHş	#;
^DB+Adj+PastPart:+dHk	#;
^DB+Adj+PresPart:+yAn	#;
^DB+Adj+Zero:+z		#;
^DB+Adverb+Adamantly:+yAsHyA	#;
^DB+Adverb+AfterDoingSo:+yHp	#;
^DB+Adverb+AsLongAs:+dHkCA	#;
^DB+Adverb+ByDoingSo:+yArAk	#;
^DB+Adverb+SinceDoingSo:+yAlH	#;
^DB+Adverb+When:+yHncA	#;
^DB+Adverb+WithoutBeingAbleToHaveDoneSo:+yAmAdAn	#;
^DB+Adverb+WithoutHavingDoneSo:+mAdAn	#;
^DB+Adverb+WithoutHavingDoneSo:+mAksHzHn	#;
^DB+Noun+ActOf:+mAcA	#;
^DB+Noun+Agt:+yHcH	#;
^DB+Noun+FeelLike:+yAsH	#;
^DB+Noun+FutPart:+yAcAk	#;
^DB+Noun+Inf1:+mAk	#;
^DB+Noun+Inf2:+mA	#;
^DB+Noun+Inf3:+yHS	#;
^DB+Noun+NotAbleState:+yAmAzlHk	#;
^DB+Noun+NotState:+mAzlHk	#;
^DB+Noun+PastPart:+dHk	#;

LEXICON Tense
+Aor:+Ar	Person1;
+Desr:+sA	Person1;
+Fut:+yAcAk	Person1;
+Imp:0		Person1;
+Narr:+mHş	Person1;
+Neces:+mAlH	Person1;
+Opt:+yA	Person1;
+Past:+dH	Person1;
+Prog1:+Hyor	Person1;
+Prog2:+mAktA	Person1;

LEXICON Person1
		Tense2;
+A1sg:+yHm	Cop;
+A2sg:+sHn	Cop;
+A3sg:0		Cop;
+A1pl:+yHz	Cop;
+A2pl:+sHnHz	Cop;
+A3pl:+lAr	A3pl;

LEXICON Tense2
		While;
+Narr:+mHş	While;
+Narr:+mHş	Person2;
+Past:+dH	Person2;
+Cond:+sA	Person2;

LEXICON While
^DB+Adverb+While:+yken	#;
^DB+Adverb+AsIf:+cAsHnA	#;

LEXICON Cop
		#;
+Cop:+dHr	#;

LEXICON A3pl
		#;
+Narr:+mHş	#;
+Past:+dH	#;
+Cond:+sA	#;
+Cop:+dHr	#;

LEXICON Person2
+A1sg:+yHm	#;
+A2sg:+sHn	#;
+A3sg:0		#;
+A1pl:+yHz	#;
+A2pl:+sHnHz	#;

END

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; General description

To a first approximation verb morphology consists of five sections:
voice, polarity, compounding, tense, person.  Voice and compounding
are optional.  There may also be a derivational suffix changing part
of speech following polarity, compounding, tense or person.

Voice: +Caus +Pass +Recip +Reflex
Ordering: +Pass | +Reflex | (+Caus|+Recip)(+Caus)*(+Pass)?

Polarity: +Pos +Neg:mA ^DB+Verb+Able+Neg:yAmA

Compounding: At most one observed. Following ^DB+Verb.
+Able:
+Almost:
+EverSince:
+Hastily:
+Repeat:
+Start:
+Stay:

Tense: +Aor +Desr +Fut +Imp +Narr +Neces +Opt +Past +Prog1 +Prog2
       +Cond +Cop
Ordering: 
(+Aor|+Fut|+Narr|+Neces|+Prog1|+Prog2)? (+Narr)? (+Cop? | +Past? +Cond?)
(+Desr) (+Narr|+Past)?
(+Imp)
(+Opt)


;;; Issues

;; Issue 1: will change the handling of degil:
;; +verb+neg+pres only seen with degil.

değil	değil+Verb+Neg+Pres+A3sg
Değil	değil+Verb+Neg+Pres+A3sg
DEĞİL	değil+Verb+Neg+Pres+A3sg
değildi	değil+Verb+Neg+Past+A3sg
değildik	değil+Verb+Neg+Past+A1pl
değildir	değil+Verb+Neg+Pres+A3sg+Cop
Değildir	değil+Verb+Neg+Pres+A3sg+Cop
değilim	değil+Verb+Neg+Pres+A1sg
DEĞİLİM	değil+Verb+Neg+Pres+A1sg
değiliz	değil+Verb+Neg+Pres+A1pl
DEĞİLİZ	değil+Verb+Neg+Pres+A1pl
değiller	değil+Verb+Neg+Pres+A3pl
değillerdi	değil+Verb+Neg+A3pl+Past
değillerdir	değil+Verb+Neg+Pres+A3pl+Cop
değillermiş	değil+Verb+Neg+A3pl+Narr
değillerse	değil+Verb+Neg+A3pl+Cond
değilmiş	değil+Verb+Neg+Narr+A3sg
değilse	değil+Verb+Neg+Cond+A3sg
Değilse	değil+Verb+Neg+Cond+A3sg
değilseniz	değil+Verb+Neg+Cond+A2pl
değilsin	değil+Verb+Neg+Pres+A2sg
değilsiniz	değil+Verb+Neg+Pres+A2pl


;; Issue 2: Will change the handling of ise etc.

idi	i	+Verb+Pos+Past+A3sg
idiler	i	+Verb+Pos+Past+A3pl
idim	i	+Verb+Pos+Past+A1sg
İdim	i	+Verb+Pos+Past+A1sg
idiyseler	i	+Verb+Pos+Past+Cond+A3pl
imişçesine	i	+Verb+Pos+Narr+A3sg^DB+Adverb+AsIf
imiş	i	+Verb+Pos+Narr+A3sg
imişler	i	+Verb+Pos+Narr+A3pl
imişsin	i	+Verb+Pos+Narr+A2sg
ise	i	+Verb+Pos+Cond+A3sg
isek	i	+Verb+Pos+Cond+A1pl
iseler	i	+Verb+Pos+Cond+A3pl
isen	i	+Verb+Pos+Cond+A2sg
İsen	i	+Verb+Pos+Cond+A2sg


;; Issue 3: upper ambiguities

Aor: +Ar +Hr +r
Cond: +sA +ysA
Caus: +Ar +dHr +Hr +Ht +t
Pass: +Hl +Hn +HnHl
Imp: +sA, 0
Narr: +ymHS +mHS
Past: +dH +ydH

;; Issue 4: lower ambiguities

+Ar: Aor Caus
+dHr: Cop Caus
+Hn: A2sg Pass Reflex
+Hr: Aor Caus
+mA: Neg Inf2
+sA: Cond Desr Imp
+sHn: A2sg A3sg
+z: A3sg A3pl


=== JUNK ===
LEXICON PersonDesr
		Tense2;
+A1sg:+m	#;
+A2sg:+n	#;
+A3sg:0		#;
+A1pl:+k	#;
+A2pl:+nHz	#;
+A3pl:+lAr	Tense2;

LEXICON PersonImp
+A2sg:0		#;
+A3sg:+sHn	#;
+A2pl:+yHn	#;
+A2pl:+yHnHz	#;
+A3pl:+sHnlAr	#;

LEXICON TenseP
		Narr;
+A3pl:+lAr	Narr;

LEXICON Narr
		While;
+Narr:+mHş	While;

LEXICON While
			Past;
^DB+Adverb+While:+yken	#;
^DB+Adverb+AsIf:+cAsHnA	#;

LEXICON Past
		Cond;
+Past:+dH	PastP;

LEXICON PastP
		Cond;
+A1sg:+m	Cond;
+A2sg:+n	Cond;
+A3sg:0		Cond;
+A1pl:+k	Cond;
+A2pl:+nHz	Cond;
+A3pl:+lAr	Cond;

LEXICON Cond
		Person;
+Cond:+ysA	Person;

LEXICON Person000
		Cop;
+A1sg:+yHm	Cop;
+A2sg:+sHn	Cop;
+A3sg:0		Cop;
+A1pl:+yHz	Cop;
+A2pl:+sHnHz	Cop;
+A3pl:+lAr	Cop;

