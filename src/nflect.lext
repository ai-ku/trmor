!! Includes all sequences following +Noun except cyclic derivational
!! suffixes following +Nom.

LEXICON Nroot
+A3sg+Pnon+Nom	Vzero?;
		Nflect;

LEXICON Nflect		!! Does not allow +A3sg+Pnon+Nom
+A3sg	Nposs1;
+A3pl	Nposs;

LEXICON Nposs1
+Pnon	Ncase-Nom;
	Nposs-Pnon;

LEXICON Nposs
+Pnon	Ncase;
	Nposs-Pnon;

LEXICON Nposs-Pnon
+P1sg	Ncase;
+P1pl	Ncase;
+P2sg	Ncase;
+P2pl	Ncase;
+P3sg	Ncase;
+P3pl	Ncase;

LEXICON Ncase
+Nom	Vzero?;
	Ncase-Nom;

LEXICON Ncase-Nom
+Abl	Vzero?;
+Acc	#;
+Dat	Vzero?;
+Equ	#;
+Gen	Vzero?;
+Gen	PronRel;
+Ins	Vzero?;
+Loc	Vzero?;
+Loc	AdjRel;


LEXICON PronRel
^DB+Pron+Rel	PRcount;

LEXICON PRcount
+A3sg	PRposs;
+A3pl	PRposs;

LEXICON PRposs
+P3sg	Ncase;
+Pnon	Ncase;

LEXICON AdjRel
^DB+Adj+Rel	NVzero?;

LEXICON NVzero?
	Nzero;
	Vzero?;

LEXICON Noun
+A3sg+Pnon+Nom	Nderiv;
+A3pl+Pnon+Nom	Nderiv2;
		Nroot;

LEXICON Nzero
^DB+Noun+Zero	Nflect;

LEXICON NounKI
+A3sg+Pnon+Nom	AdjRel;
		Noun;

LEXICON NounP1
+A3pl+P1sg	Ncase;

LEXICON NounP2
+A3pl+P2sg	Ncase;

LEXICON NounP3
		Noun;

LEXICON NounPL
+A3pl		Nposs;


