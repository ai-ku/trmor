!! Includes all sequences following +Noun except cyclic derivational
!! suffixes following +Nom.

Multichar_Symbols
	+Abl +Able +Acc +Acquire +ActOf +Adamantly +Adj +Adverb
	+AfterDoingSo +Agt +Almost +Aor +AorPart +AsIf +AsLongAs +A1pl
	+A1sg +A2pl +A2sg +A3pl +A3sg +Become +ByDoingSo +Card +Caus
	+Cond +Conj +Continue +Cop +Dat +Demons +Desr +Det +Dim +Dist
	+Dup +Equ +EverSince +FeelLike +Fut +FutPart +Gen +Hastily +Imp
	+InBetween +Inf1 +Inf2 +Inf3 +Ins +Interj +JustLike +Loc +Ly
	+Narr +NarrPart +Neces +Neg +Ness +Nom +NotAbleState +NotState
	+Noun +Num +Opt +Ord +PCAbl +PCAcc +PCDat +PCGen +PCIns +PCNom
	+Pass +Past +PastPart +Pers +Pnon +Pos +Postp +Pres +PresPart
	+Prog1 +Prog2 +Pron +Prop +P1pl +P1sg +P2pl +P2sg +P3pl +P3sg
	+Quant +Ques +Recip +Reflex +Rel +Related +Repeat +Since
	+SinceDoingSo +Start +Stay +Verb +When +While +With +Without
	+WithoutBeingAbleToHaveDoneSo +WithoutHavingDoneSo +Zero ^DB
	+Narr2 +Past2 +Imp2 +WithoutHavingDoneSo2
	^Nfinal ^Vzero

LEXICON Noun
	Ncount;

LEXICON Ncount
+A3sg:0		Nposs;
+A3pl:+lAr	Nposs;

LEXICON Nposs
+P1sg:+Hm	Ncase;
+P1pl:+HmHz	Ncase;
+P2sg:+Hn	Ncase;
+P2pl:+HnHz	Ncase;
+P3sg:+sH	Ncase;
+P3pl:+lArH	Ncase;
+Pnon:0		Ncase;

LEXICON Ncase
+Abl:+nDAn	Vzero?;
+Acc:+nH	#;
+Dat:+nA	Vzero?;
+Equ:+nCA	#;
+Gen:+nHn	Vzero?;
+Gen:+nHn	PronRel;
+Ins:+ylA	Vzero?;
+Loc:+nDA	Vzero?;
+Loc:+nDA	AdjRel;
+Nom:0		Vzero?;


LEXICON PronRel
^DB+Pron+Rel:+ki	Nfinal;
^DB+Pron+Rel:+ki	JustLike;
^DB+Pron+Rel:+ki	Vzero?;

LEXICON AdjRel
^DB+Adj+Rel:+ki		Nzero?;

LEXICON JustLike
^DB+Adj+JustLike:+sH	#;	!! BUG: This should be +AsH or +msH or something.

LEXICON Vzero?
^DB+Verb+Zero	Vzero;
	#;

LEXICON Nzero?
^DB+Noun+Zero	Nfinal;
	#;

LEXICON Nfinal
^Nfinal	#;

LEXICON Vzero
^Vzero	#;


END;
