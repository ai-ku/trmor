!! Includes all sequences following +Noun except cyclic derivational
!! suffixes following +Nom.

Multichar_Symbols
       +Abl +Acc +Adj +A3pl +A3sg +Dat +Equ +Gen +Ins +Loc +Nom +Noun 
       +Pnon +Pron +P1pl +P1sg +P2pl +P2sg +P3pl +P3sg +Rel +Verb +Zero
       ^DB ^Vzero


LEXICON Root
	Ncount;

LEXICON Ncount
+A3sg	Nposs;
+A3pl	Nposs;

LEXICON Nposs
+P1sg	Ncase;
+P1pl	Ncase;
+P2sg	Ncase;
+P2pl	Ncase;
+P3sg	Ncase;
+P3pl	Ncase;
+Pnon	Ncase;

LEXICON Ncase
+Abl	Vzero?;
+Acc	#;
+Dat	Vzero?;
+Equ	#;
+Gen	Vzero?;
+Gen	PronRel;
+Ins	Vzero?;
+Loc	Vzero?;
+Loc	AdjRel;
+Nom	Vzero?;


LEXICON PronRel
^DB+Pron+Rel	PRcount;

LEXICON PRcount
+A3sg	PRposs;
+A3pl	PRposs;

LEXICON PRposs
+P3sg	Ncase;
+Pnon	Ncase;

LEXICON AdjRel
^DB+Adj+Rel	Nzero?;

LEXICON Vzero?
	Vzero;
	#;

LEXICON Vzero
^DB+Verb+Zero	AfterVzero;

LEXICON AfterVzero
^Vzero	#;

LEXICON Nzero?
	Nzero;
	#;

LEXICON Nzero
^DB+Noun+Zero	AfterNzero;

LEXICON AfterNzero
+A3sg	Nposs1;
+A3pl	Nposs2;

LEXICON Nposs1
+P1sg	Ncase1;
+P1pl	Ncase1;
+P2sg	Ncase1;
+P2pl	Ncase1;
+P3sg	Ncase1;
+P3pl	Ncase1;
+Pnon	Ncase0;

LEXICON Nposs2
+P1sg	Ncase2;
+P1pl	Ncase2;
+P2sg	Ncase2;
+P2pl	Ncase2;
+P3sg	Ncase2;
+P3pl	Ncase2;
+Pnon	Ncase2;

LEXICON Ncase0
+Abl	#;
+Acc	#;
+Dat	#;
+Gen	#;
+Ins	#;
+Loc	#;

LEXICON Ncase1
+Abl	#;
+Acc	#;
+Dat	#;
+Gen	#;
+Ins	#;
+Loc	#;
+Nom	#;

LEXICON Ncase2
+Abl	#;
+Acc	#;
+Dat	#;
+Equ	#;
+Gen	#;
+Ins	#;
+Loc	#;
+Nom	#;

END;
