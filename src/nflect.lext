!! Includes all sequences following +Noun except cyclic derivational
!! suffixes following +Nom.

Multichar_Symbols
	+Abl +Able +Acc +Acquire +ActOf +Adamantly +Adj +Adverb
	+AfterDoingSo +Agt +Almost +Aor +AorPart +AsIf +AsLongAs +A1pl
	+A1sg +A2pl +A2sg +A3pl +A3sg +Become +ByDoingSo +Card +Caus
	+Cond +Conj +Continue +Cop +Dat +Demons +Desr +Det +Dim +Dist
	+Dup +Equ +EverSince +FeelLike +Fut +FutPart +Gen +Hastily +Imp
	+InBetween +Inf1 +Inf2 +Inf3 +Ins +Interj +JustLike +Loc +Ly
	+Narr +NarrPart +Neces +Neg +Ness +Nom +NotAbleState +NotState
	+Noun +Num +Opt +Ord +PCAbl +PCAcc +PCDat +PCGen +PCIns +PCNom
	+Pass +Past +PastPart +Pers +Pnon +Pos +Postp +Pres +PresPart
	+Prog1 +Prog2 +Pron +Prop +P1pl +P1sg +P2pl +P2sg +P3pl +P3sg
	+Quant +Ques +Recip +Reflex +Rel +Related +Repeat +Since
	+SinceDoingSo +Start +Stay +Verb +When +While +With +Without
	+WithoutBeingAbleToHaveDoneSo +WithoutHavingDoneSo +Zero ^DB
	+Narr2 +Past2 +Imp2 +WithoutHavingDoneSo2
	^Nzero ^Vzero
	 

LEXICON Noun
	Ncount;

LEXICON Ncount
+A3sg	Nposs;
+A3pl	Nposs;

LEXICON Nposs
+P1sg	Ncase;
+P1pl	Ncase;
+P2sg	Ncase;
+P2pl	Ncase;
+P3sg	Ncase;
+P3pl	Ncase;
+Pnon	Ncase;

LEXICON Ncase
+Abl:+nDAn	Vzero?;
+Acc:+nH	#;
+Dat:+nA	Vzero?;
+Equ:+nCA	#;
+Gen:+nHn	Vzero?;
+Gen:+nHn	PronRel;
+Ins:+ylA	Vzero?;
+Loc:+nDA	Vzero?;
+Loc:+nDA	AdjRel;
+Nom:0		Vzero?;


LEXICON PronRel
^DB+Pron+Rel:+ki	PRcount;

LEXICON PRcount
+A3sg	PRposs;
+A3pl	PRposs;

LEXICON PRposs
+P3sg	Ncase;
+Pnon	Ncase;

LEXICON AdjRel
^DB+Adj+Rel:+ki		Nzero?;

LEXICON Vzero?
	Vzero;
	#;

LEXICON Vzero
^DB+Verb+Zero	AfterVzero;

LEXICON AfterVzero
^Vzero	#;

LEXICON Nzero?
	Nzero;
	#;

LEXICON Nzero
^DB+Noun+Zero	AfterNzero;

LEXICON AfterNzero
^Nzero	#;


END;
