Multichar_Symbols
	+Abl +Able +Acc +Acquire +ActOf +Adamantly +Adj +Adverb
	+AfterDoingSo +Agt +Almost +Aor +AorPart +AsIf +AsLongAs +A1pl
	+A1sg +A2pl +A2sg +A3pl +A3sg +Become +ByDoingSo +Card +Caus
	+Cond +Conj +Continue +Cop +Dat +Demons +Desr +Det +Dim +Dist
	+Dup +Equ +EverSince +FeelLike +Fut +FutPart +Gen +Hastily +Imp
	+InBetween +Inf1 +Inf2 +Inf3 +Ins +Interj +JustLike +Loc +Ly
	+Narr +NarrPart +Neces +Neg +Ness +Nom +NotAbleState +NotState
	+Noun +Num +Opt +Ord +PCAbl +PCAcc +PCDat +PCGen +PCIns +PCNom
	+Pass +Past +PastPart +Pers +Pnon +Pos +Postp +Pres +PresPart
	+Prog1 +Prog2 +Pron +Prop +P1pl +P1sg +P2pl +P2sg +P3pl +P3sg
	+Quant +Ques +Recip +Reflex +Rel +Related +Repeat +Since
	+SinceDoingSo +Start +Stay +Verb +When +While +With +Without
	+WithoutBeingAbleToHaveDoneSo +WithoutHavingDoneSo +Zero ^DB
	+Narr2 +Past2 +Imp2 +WithoutHavingDoneSo2

LEXICON Root	
+Verb:gül	Verb;


LEXICON Verb
	Voice;

LEXICON Voice
			Polarity;
^DB+Verb+Pass:+Hl	Polarity;
^DB+Verb+Caus:+t	Voice3;
^DB+Verb+Recip:+Hş	Voice2;
^DB+Verb+Reflex:+Hn	Voice2;		!! See Issue 8.

LEXICON Voice3
			Polarity;
^DB+Verb+Pass:+Hl	Polarity;
^DB+Verb+Caus:+t	Voice2;

LEXICON Voice2
			Polarity;
^DB+Verb+Pass:+Hl	Polarity;
^DB+Verb+Caus:+t	Voice1;

LEXICON Voice1
			Polarity;
^DB+Verb+Pass:+Hl	Polarity;

LEXICON Polarity
+Pos:0			Agt;
+Pos:0			Compound3;
+Neg:+mA		Compound2;
^DB+Verb+Able+Neg:+yAmA	Compound1;

LEXICON Compound3
^DB+Verb+Start:+yAkoy		Deriv;
^DB+Verb+Stay:+yAkal		Deriv;
^DB+Verb+Almost:+yAyaz		Deriv;
^DB+Verb+EverSince:+yAgel	Deriv;
				Compound2;

LEXICON Compound2
^DB+Verb+Continue:+yAgit	Deriv;  !! BUG:  t-d conversion is not fixed
^DB+Verb+Repeat:+yAdur		Deriv;
^DB+Verb+Hastily:+yHver		HastilyNeg;
				Compound1;

LEXICON Compound1
^DB+Verb+Able:+yAbil		Deriv;
				Deriv;

LEXICON HastilyNeg		
+Neg+Aor:+mA+Hr	CNP|Person;		!! BUG: This is Oflazer's hack
		Deriv;	

LEXICON Agt
^DB+Adj+Agt:+yHcH	Adj;
^DB+Noun+Agt:+yHcH	Noun;

LEXICON Deriv
			Tense;
^DB+Adj+AorPart:+Hr	Adj;
^DB+Adj+FeelLike:+yAsH	#;
^DB+Adj+FutPart:+yAcAk	AdjPoss;
^DB+Adj+NarrPart:+mHş	Adj;
^DB+Adj+PastPart:+DHk	AdjPoss;
^DB+Adj+PresPart:+yAn	Adj;
^DB+Adverb+Adamantly:+yAsHyA	#;
^DB+Adverb+AfterDoingSo:+yHp	#;
^DB+Adverb+AsLongAs:+DHkçA	#;
^DB+Adverb+ByDoingSo:+yArAk	#;
^DB+Adverb+SinceDoingSo:+yAlH	Noun0?;
^DB+Adverb+When:+yHncA		Noun0?;
^DB+Adverb+WithoutBeingAbleToHaveDoneSo:+yAmAdAn	#;
^DB+Adverb+WithoutHavingDoneSo:+mAdAn		#;
^DB+Adverb+WithoutHavingDoneSo2:+mAksHzHn	#;
^DB+Noun+ActOf:+mAcA		Noun;
^DB+Noun+FeelLike:+yAsH		Noun;
^DB+Noun+FutPart:+yAcAk		Noun;
^DB+Noun+Inf1:+mAk		Noun;
^DB+Noun+Inf2:+mA		Noun;
^DB+Noun+Inf3:+yHş		Noun;
^DB+Noun+NotAbleState:+yAmAzlHk	Noun;
^DB+Noun+NotState:+mAzlHk	Noun;
^DB+Noun+PastPart:+DHk		Noun;

LEXICON Tense
+Aor:+Hr	CNP|Person;
+Aor:+Hr	AorAdv;
+Desr:+sA	NP|Person2;
+Fut:+yAcAk	CNP|Person+Cop;
+Imp:0		PersonImp;
+Imp2:+sA	PersonImpSA;
+Narr:+mHş	CNP|Person+Cop;
+Narr:+mHş	NarrAdv1;
+Neces:+mAlH	CNP|Person+Cop;
+Opt:+yA	NP|Person1;
+Past:+DH	CP|Person;
+Prog1:+Hyor	CNP|Person+Cop;
+Prog2:+mAktA	CNP|Person+Cop;

LEXICON CNP|Person+Cop
		CNP+Person;
		Person1+Cop?;

LEXICON CNP|Person
		CNP+Person;
		Person1;

LEXICON CP|Person
		C+Person;
		Person2+CP?;

LEXICON NP|Person1
		NP+Person;
		Person1;

LEXICON NP|Person2
		NP+Person;
		Person2;

LEXICON CNP+Person
+A3pl:+lAr	CNP;
+Cond:+ysA	Person2;
+Past2:+yDH	Person2;
+Narr2:+ymHş	Person1+Cop?;
+Narr2:+ymHş	NarrAdv2;
		While;

LEXICON CNP
+Cond:+ysA	#;
+Narr2:+ymHş	#;
+Past2:+yDH	#;
		While;

LEXICON NP+Person
+A3pl:+lAr	NP;
+Narr2:+ymHş	Person1;
+Past2:+yDH	Person2;

LEXICON NP
+Narr2:+ymHş	#;
+Past2:+yDH	#;

LEXICON C+Person
+Cond:+ysA	Person2;

LEXICON Person1
+A1sg:+yHm	#;
+A2sg:+sHn	#;
+A3sg:0		#;
+A1pl:+yHz	#;	!! Should be +lHm following +Opt - fixed with regex
+A2pl:+sHnHz	#;
+A3pl:+lAr	#;

LEXICON Person1+Cop?
+A1sg:+yHm	Cop?;
+A2sg:+sHn	Cop?;
+A3sg:0		Cop?;
+A1pl:+yHz	Cop?;
+A2pl:+sHnHz	Cop?;
+A3pl:+lAr	Cop?;

LEXICON Person2
+A1sg:+m  	#;
+A2sg:+n  	#;
+A3sg:0	  	#;
+A1pl:+k  	#;
+A2pl:+nHz	#;
+A3pl:+lAr	#;

LEXICON Person2+CP?
+A1sg:+m  	CP?;
+A2sg:+n  	CP?;
+A3sg:0		#;   !! No need for A3sg here, put at the end.
!+A3sg:0  	C?;  !! BUG: I think this is oflazer's bug should be CP?
+A1pl:+k  	CP?;
+A2pl:+nHz	CP?;
+A3pl:+lAr	CP?;

LEXICON PersonImp
+A2sg:0		#;
+A3sg:+sHn	#;
+A2pl:+yHn	#;
+A2pl:+yHnHz	#;
+A3pl:+sHnlAr	#;

LEXICON PersonImpSA
+A2sg:+nA	#;
+A2pl:+nHzA	#;

LEXICON NarrAdv1
+A3sg:0		AsIf;
+A3pl:+lAr	AsIf;

LEXICON NarrAdv2
+A3sg:0		AsIf;
+A3pl:+lAr	While;

LEXICON AorAdv
+A3sg:0		AsIf;

LEXICON While
^DB+Adverb+While:+yken	AdjRel?;

LEXICON AsIf
^DB+Adverb+AsIf:+CAsHnA	#;

LEXICON Cop?
+Cop:+DHr	#;
		#;

LEXICON CP?
+Cond:+ysA	#;
+Past2:+yDH	#;
		#;

! LEXICON C?
! +Cond:+ysA	#;
!		#;


LEXICON Noun
	Ncount;
+Prop:0	Ncount;

LEXICON Ncount
+A3sg:0		Nposs;
+A3pl:+lAr	Nposs;

LEXICON Nposs
+P1sg:+Hm	Ncase;
+P1pl:+HmHz	Ncase;
+P2sg:+Hn	Ncase;
+P2pl:+HnHz	Ncase;
+P3sg:+sH	Ncase3;
+P3pl:+lArH	Ncase3;
+Pnon:0		Ncase;

LEXICON Ncase
+Abl:+DAn	Verb0?;
+Acc:+yH	#;
+Dat:+yA	Verb0?;
+Loc:+DA	Loc;
		Ncase0;

LEXICON Ncase3
+Abl:+nDAn	Verb0?;
+Acc:+nH	#;
+Dat:+nA	Verb0?;
+Loc:+nDA	Loc;
		Ncase0;

LEXICON Ncase0
+Equ:+CA	#;
+Gen:+nHn	Gen;
+Ins:+ylA	Verb0?;
+Nom:0		Nom;

LEXICON Gen
^DB+Pron+Rel:+ki	Noun;
			Verb0?;

LEXICON Loc
^DB+Adj+Rel:+ki		Adj;
			Verb0?;

LEXICON Nom
^DB+Adj+InBetween:+arası	Adj;
^DB+Adj+Rel:+ki		Adj;
^DB+Adj+Related:+sAl	Adj;
^DB+Adj+With:+lH	Adj;
^DB+Adj+Without:+sHz	Adj;
^DB+Adverb+Since:+dHr	#;
^DB+Noun+Dim:+CHk	Noun;
			AdjNom;

LEXICON Adj
^DB+Adj+AsIf:+CA	Adj;
^DB+Adverb+Ly:+CA	#;
^DB+Noun+Zero:0		Noun;
			AdjNom;

LEXICON AdjNom
^DB+Adj+JustLike:+sH	Adj;
^DB+Noun+Agt:+CH	Noun;
^DB+Noun+Ness:+lHk	Noun;
^DB+Verb+Acquire:+lAn	Verb;
^DB+Verb+Become:+lAş	Verb;
			Verb0?;

LEXICON AdjPoss
+P1sg:+Hm	#;
+P1pl:+HmHz	#;
+P2sg:+Hn	#;
+P2pl:+HnHz	#;	
+P3sg:+sH	#;
+P3pl:+lArH	#;
+Pnon:0		#;

LEXICON Verb0?
^DB+Verb+Zero:0	AfterVerb0;
		#;
		
LEXICON AfterVerb0
+Pres:0		Person1+Cop?;
		CNP+Person;
		AsIf;

LEXICON Noun0?
^DB+Noun+Zero:0	Noun;
		#;

LEXICON AdjRel?
^DB+Adj+Rel:+ki	#;
		#;

END
